<?rev
put gBASEPATH into gBASEPATH

if gBASEPATH is "gBASEPATH" then
	put "No direct script access allowed."
	exit to top
end if

##
# revIgniter
#
# An application development framework for LiveCode
# modeled on CodeIgniter.
#
# @package		revIgniter
# @author		  rabit@revigniter.com
# @copyright	Copyright (c) 2009 - 2016, dimensionB Bitter u. Bitter GmbH
# @license		http://revigniter.com/userGuide/license.html
# @link		    http://revigniter.com
##

## ------------------------------------------------------------------------

##
# revIgniter Cookie Helpers
#
# @package		revIgniter
# @subpackage	Helpers
# @category	  Helpers
# @author		  rabit@revigniter.com
# @link		    http://revigniter.com/userGuide/helpers/cookieHelper.html
##

## ------------------------------------------------------------------------




/*----------------------------------------------------------------------
--| COMMAND rigSetCookie
--|
--| Author: rabit
--| Version:  1.4
--| Created: 30-05-09
--| Last Mod: 02-07-13
--| Requires: Input.lc, _rigInputSetCookie
--|
--| Summary: Wrapper for _rigInputSetCookie in Input library. Accepts nine parameters, or you
--| 	       can submit an associative array in the first parameter containing all the values.
--| 				 Please see the User Guide for key names or _rigInputSetCookie in Input.lc.
--| 
--| Format:  rigSetCookie param1, param2, param3, param4, param5, param6, param7, param8, param9
--|
--| Parameters: string <pName>, mixed <pValue> can be a string or an array, integer <pExp>, string <pDomain> usually: .yourdomain.com,
--|               string <pPath>, string <pPrefix>, bool <pReplace>, bool <pSecure>, bool <pHttpOnly>
--|
--| Return: empty
----------------------------------------------------------------------*/

command rigSetCookie pName, pValue, pExp, pDomain, pPath, pPrefix, pReplace, pSecure, pHttpOnly
	_rigInputSetCookie pName, pValue, pExp, pDomain, pPath, pPrefix, pReplace, pSecure, pHttpOnly
end rigSetCookie








/*----------------------------------------------------------------------
--| FUNCTION rigGetCookie
--|
--| Author: rabit
--| Version:  1.6
--| Created: 30-05-09
--| Last Mod: 03-07-16
--| Requires: Input.lc, rigGetCookies(), rigConfigItem(), rigVarCookie()
--|
--| Summary: Fetch an item from the COOKIE array.
--| 
--| Format:  rigGetCookie(param1, param2)
--|
--| Parameters: string <pName>, bool <pXssFlag>
--|
--| Return: string
----------------------------------------------------------------------*/

function rigGetCookie pName pXssFlag
	if pXssFlag is empty then
		put FALSE into tXssFlag
	else
		put pXssFlag into tXssFlag
	end if

	put empty into tPrefix
	put rigGetCookies() into tCookiesArray

	if (pName is not among the keys of tCookiesArray) and (rigConfigItem("cookiePrefix") is not empty) then
		put rigConfigItem("cookiePrefix") into tPrefix 
	end if

	put rigVarCookie(tPrefix & pName, pXssFlag) into tCookieVal
	
  put urlDecode(tCookieVal) into tURLdecodedCookieVal
  if tURLdecodedCookieVal <> false then
    put base64Decode(tURLdecodedCookieVal) into tDecodedCookieVal
		# IF THE VALUE IS MARKED AS ARRAY DECODE IT
    if char 1 to 2 of tDecodedCookieVal is "!a" then
      if (char 1 to 3 of tDecodedCookieVal is ("!a" & rigNumToCodepoint(1))) and (char -3 to -1 of tDecodedCookieVal is (rigNumToCodepoint(1) & "a!")) then
        put arrayDecode(char 4 to -4 of tDecodedCookieVal) into tDecodedCookieVal
      end if
    end if
    return tDecodedCookieVal
  end if

  return false
end rigGetCookie







/*----------------------------------------------------------------------
--| FUNCTION rigGetCookies
--|
--| Author: Sarah Reichelt
--| Modification: rabit
--| Version:  1.2
--| Created: 30-05-09
--| Last Mod: 13-01-15
--| Requires: --
--|
--| Summary: Fetch all cookies stored for this site / page.
--| 
--| Format:  rigGetCookies()
--|
--| Parameters: --
--|
--| Return: array
----------------------------------------------------------------------*/

function rigGetCookies
	if gCOOKIE is not empty then
		put gCOOKIE into tCookiesRaw
	else
		put $_SERVER["HTTP_COOKIE"] into tCookiesRaw
	end if

	set the itemDel to "="
	if tCookiesRaw contains ";" then
		set the lineDel to ";"
		repeat for each line cookie in tCookiesRaw
		  put item 1 of cookie into tCookieName
			if char 1 of tCookieName = space then delete char 1 of tCookieName
        put base64Decode(urlDecode(item 2 of cookie)) into tCookies[tCookieName]
      
		end repeat
	else
		put item 1 of tCookiesRaw into tCookieName
		if char 1 of tCookieName = space then delete char 1 of tCookieName
      put base64Decode(urlDecode(item 2 of tCookiesRaw)) into tCookies[tCookieName]
	end if

	return tCookies
end rigGetCookies








/*----------------------------------------------------------------------
--| COMMAND rigDeleteCookie
--|
--| Author: rabit
--| Version:  1.0
--| Created: 30-05-09
--| Last Mod: 30-05-09
--| Requires: --
--|
--| Summary: Same as rigSetCookie with value and expiration set to empty
--| 
--| Format:  rigDeleteCookie param1, param2, param3, param4
--|
--| Parameters: string <pName>, string <pDomain> usually: .yourdomain.com, string <pPath>, string <pPrefix>
--|
--| Return: empty
----------------------------------------------------------------------*/

command rigDeleteCookie pName, pDomain, pPath, pPrefix
	rigSetCookie pName, "", "", pDomain, pPath, pPrefix
end rigDeleteCookie








--| END OF cookieHelper.lc
--| Location:  ./system/helpers/cookieHelper.lc
----------------------------------------------------------------------