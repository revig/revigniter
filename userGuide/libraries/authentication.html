<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset="utf-8">
		<meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
		<title>Authentication : revIgniter User Guide</title>
		<meta name="viewport" content="width=device-width, initial-scale=1.0">
		<meta name="robots" content="all" />
		<meta name="author" content="Ralf Bitter" />
		<meta name='description' content='revIgniter User Guide' />

		<!-- Bootstrap -->
		<link rel="stylesheet" media="screen" href="../css/bootstrap.min.css">

		<script src="../js/modernizr-2.6.2-respond-1.1.0.min.js"></script>
		<script src="../js/nav-ck.js"></script>
		
		<!-- Touch icons -->
		<link rel="apple-touch-icon-precomposed" sizes="144x144" href="../images/apple-touch-icon-144-precomposed.png">
		<link rel="apple-touch-icon-precomposed" sizes="114x114" href="../images/apple-touch-icon-114-precomposed.png">
		<link rel="apple-touch-icon-precomposed" sizes="72x72" href="../images/apple-touch-icon-72-precomposed.png">
		<link rel="apple-touch-icon-precomposed" href="../images/apple-touch-icon-57-precomposed.png">
		
		<!-- HTML5 shim and Respond.js IE8 support of HTML5 elements and media queries -->
		<!--[if lt IE 9]>
		    <script src="../js/html5shiv.js"></script><a href="models.html" id="" title="models">models</a>
		    <script src="../js/respond.min.js"></script>
		<![endif]-->

	</head>
<body>
	<!-- START PAGE HEAD -->
	<div class="navbar navbar-inverse navbar-fixed-top" role="navigation">
      <div class="container">
        <div class="navbar-header">
          <button type="button" class="navbar-toggle" data-toggle="collapse" data-target=".navbar-collapse">
            <span class="icon-bar"></span>
            <span class="icon-bar"></span>
            <span class="icon-bar"></span>
          </button>
    			<a class="navbar-brand" href="#">revIgniter User Guide Version 1.9.4</a>
        </div>

        <div class="collapse navbar-collapse">
          <form class="navbar-form navbar-right" role="search" method="get" action="http://www.google.com/search">
 		      <div class="form-group">
 				<input type="hidden" name="as_sitesearch" id="as_sitesearch" value="revigniter.com/userGuide/" />
 		        <span id="searchLabel">Search User Guide</span> &nbsp; <input type="search" placeholder="Search ..." name="q" id="q" class="revignitorsearch" accesskey="s" autosave="bsn_srch" results="5" />
 				&nbsp;<input type="image" id="submitSearch" src="../images/searchGo.png" alt="Search Go Button" />
 		      </div>
 		 	 		</form>
        </div><!--/.nav-collapse -->
     </div>
		 
		 <!-- START BREADCRUMB -->		 
		 <div id="breadcrumbContainer">
			 <div class="container">
			 		<ol class="breadcrumb">
			 			<li><a href="http://revigniter.com/">revIgniter Home</a></li>
			 		 	<li><a href="../index.html">User Guide Home</a></li>
			 		 	<li class="active">Authentication</li>
						<li id="tocListItem" class="pull-right"><a href="../toc.html">Table of Contents Page</a></li>
			 		 </ol>
				</div>
		</div>
		<!-- END BREADCRUMB -->		
		
		<!-- START SLIDEBOX -->
		<div class="slide-panel top">
			<div class="container">
				<div id="panelContent"><script type="text/javascript">create_menu('../');</script></div>
			</div>
			<div id="open-button" class="slide-button">Table of Contents</div>
			<div id="close-button" class="slide-button">Close</div>
		</div>
		<!-- END SLIDEBOX -->
	 
  </div>
<!-- END PAGE HEAD -->

 

<!-- START CONTENT -->
<div class="container">
	
<div id="content">

<h1>Authentication Library</h1>

<p>revIgniter provides a lightweight, simple, and clean authentication library which is based on Ion Auth for CodeIgniter by Ben Edmunds, ben.edmunds@gmail.com.</p>


<ul>
<li><a href="#overview">Overview</a></li>
<li><a href="#configuration">Authentication Configuration</a></li>
<li><a href="#handlerreference">Handler Reference</a></li>
<li><a href="#hooks">Authentication Hooks</a></li>
<li><a href="#tables">Authentication Tables</a></li>
<li><a href="#emailtemplates">Email Templates</a></li>
<li><a href="#usage">Usage Example</a>

	<ul>
		<li><a href="#thecontroller">The Controller</a></li>
		<li><a href="#thevalidationfile">The Form Validation File</a></li>
		<li><a href="#theviewfiles">The View Files</a></li>
		<li><a href="#cssfile">The Css File</a></li>
	</ul>
</li>
</ul>


<h2>Initializing the Library</h2>

<p>Like most other libraries in revIgniter, the Authentication library is initialized in your controller using the <dfn>rigLoaderLoadLibrary</dfn> handler:</p>

<pre><code class="lang-livecodeserver">rigLoaderLoadLibrary "Authentication"</code></pre>

<p class="important"><strong>Note:</strong> In order to use authentication, you must first create the appropriate database tables for this purpose as outlined in the <a href="#tables">Authentication Tables</a> section below.</p>

<p>&nbsp;</p>

<a name="overview"></a>
<h2>Overview</h2>

<p>Following below the features and key points of revIgniter's Authentication library:</p>

<ul>
	<li>Highly configurable.</li>
	<li>Support for "hooks" to modify the inner workings of the library.</li>
	<li>Autonomous encryption settings independent from the core settings.</li>
	<li>Basic auth options (login, logout, register, change password).</li>
	<li>Remember me.</li>
	<li>Login using email address or any other data stored in the Users table (depending on config settings).</li>
	<li>Forgot password.</li>
	<li>Logging last login time and IP-address. </li>
	<li>Email or manual activation.</li>
	<li>Counting login attempts.</li>
	<li>Expiring forgotten password requests.</li>
</ul>



<p>&nbsp;</p>

<a name="configuration"></a>
<h2>Authentication Configuration</h2>


<p>The following is a list of all the preferences that can be set in system/application/config/authentication.lc:</p>

<div class="table">
<table>
<tr>
<th>Preference</th>
<th>Default&nbsp;Value</th>
<th>Options</th>
<th>Description</th>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["tables"]["users"]</strong></td><td class="td">Users</td><td class="td">None</td><td class="td">The table name to use for the users table.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["tables"]["groups"]</strong></td><td class="td">Groups</td><td class="td">None</td><td class="td">The table name to use for the groups table.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["tables"]["usersGroups"]</strong></td><td class="td">UsersGroups</td><td class="td">None</td><td class="td">The table name to use for the users groups table.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["tables"]["loginAttempts"]</strong></td><td class="td">LoginAttempts</td><td class="td">None</td><td class="td">The table name to use for the login attempts table.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["join"]["users"]</strong></td><td class="td">userId</td><td class="td">None</td><td class="td">Users table column you want to join WITH.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["join"]["groups"]</strong></td><td class="td">groupId</td><td class="td">None</td><td class="td">Group table column you want to join WITH.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["cipher"]</strong></td><td class="td">bf</td><td class="td">See LiveCode's cipherNames() function.</td><td class="td">Cipher used to encrypt passwords (see revIgniter's <a href="encryption.html">Encryption Library</a>).</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["keyLength"]</strong></td><td class="td">128</td><td class="td">See LiveCode's cipherNames() function.</td><td class="td">Key length used to encrypt passwords (see revIgniter's <a href="encryption.html">Encryption Library</a>).</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["authEncryptionKey"]</strong></td><td class="td">myKey</td><td class="td">None</td><td class="td">Authentication encryption key (see revIgniter's <a href="encryption.html">Encryption Library</a>).</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["useAuthEncryptionKey"]</strong></td><td class="td">TRUE</td><td class="td">TRUE or FALSE (boolean)</td><td class="td">Set this to FALSE if you prefere to use revIgniter's universal encryption key as set in config.lc.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["siteTitle"]</strong></td><td class="td">Example.com</td><td class="td">None</td><td class="td">The title of your site, used for email.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["adminEmail"]</strong></td><td class="td">admin@example.com</td><td class="td">None</td><td class="td">Your administrator email address.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["minLengthPassword"]</strong></td><td class="td">8</td><td class="td">None</td><td class="td">Minimum required password length.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["maxLengthPassword"]</strong></td><td class="td">20</td><td class="td">None</td><td class="td">Maximum allowed password length.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["emailActivation"]</strong></td><td class="td">TRUE</td><td class="td">TRUE or FALSE (boolean)</td><td class="td">Sets whether to require email user activation or not.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["manualActivation"]</strong></td><td class="td">FALSE</td><td class="td">TRUE or FALSE (boolean)</td><td class="td">Sets whether to require manual user activation or not.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["identityColumn"]</strong></td><td class="td">email</td><td class="td">None</td><td class="td">Column to use for uniquely identifing users logging in etc. You should add an index in the users table for whatever you set this option to.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["defaultGroup"]</strong></td><td class="td">members</td><td class="td">None</td><td class="td">Name of the default user group.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["defaultAdminGroup"]</strong></td><td class="td">admin</td><td class="td">None</td><td class="td">Name of the admin group.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["rememberUsers"]</strong></td><td class="td">TRUE</td><td class="td">TRUE or FALSE (boolean)</td><td class="td">Allow users to be remembered and enable auto-login.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["autoLoginExpire"]</strong></td><td class="td">129600</td><td class="td">None</td><td class="td">How long to remember the user for in seconds. Set to zero for no expiration.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["userExtendOnLogin"]</strong></td><td class="td">FALSE</td><td class="td">TRUE or FALSE (boolean)</td><td class="td">Extend the users session expiration everytime they auto-login.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["trackLoginAttempts"]</strong></td><td class="td">TRUE</td><td class="td">TRUE or FALSE (boolean)</td><td class="td">Track the number of failed login attempts for each user or ip.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["maxLoginAttempts"]</strong></td><td class="td">3</td><td class="td">None</td><td class="td">The maximum number of failed login attempts. This maximum is not enforced by the library. If set to 0, there is no maximum.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["forgotPasswordExpiration"]</strong></td><td class="td">0</td><td class="td">None</td><td class="td">The number of seconds after which a forgot password request will expire. If set to 0, forgot password requests will not expire.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["enableAuthenticationHooks"]</strong></td><td class="td">FALSE</td><td class="td">TRUE or FALSE (boolean)</td><td class="td">Enable / disable authentication "hooks".</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["sendEmails"]</strong></td><td class="td">TRUE</td><td class="td">TRUE or FALSE (boolean)</td><td class="td">Send emails using revIgniters Email.lc library. False means: The library returns identity, user id, email address and activation code.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["emailConfig"]</strong></td><td class="td">mailtype: html, protocol: sendmail</td><td class="td">file or revIgniter's <a href="email.html">Email Library prefs</a></td><td class="td">Use "file" if you want emails to be sent using revIgniter's default configuration or a config file. Use an array to manually set your email configuration.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["pathToEmailTemplates"]</strong></td><td class="td"></td><td class="td">None</td><td class="td">	Path to your authentication mail templates relative to system/application/views/. Leave this BLANK unless you would like to set something other than the default emailTemplates/ folder.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["emailActivate"]</strong></td><td class="td">authActivate.lc</td><td class="td">None</td><td class="td">Name of your account activation email template.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["emailForgotPassword"]</strong></td><td class="td">forgotPassword.lc</td><td class="td">None</td><td class="td">Name of your forgotten password email template.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["messagePrefix"]</strong></td><td class="td">&lt;p></td><td class="td">None</td><td class="td">Starting delimiter for authentication related messages.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["messageSuffix"]</strong></td><td class="td">&lt;/p></td><td class="td">None</td><td class="td">Ending delimiter for authentication related messages.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["errorPrefix"]</strong></td><td class="td">&lt;p></td><td class="td">None</td><td class="td">Starting delimiter for authentication related error messages.</td>
</tr><tr>
<td class="td"><strong>sAuthenticationConf["errorSuffix"]</strong></td><td class="td">&lt;/p></td><td class="td">None</td><td class="td">Ending delimiter for authentication related error messages.</td>
</tr>
</table>
</div>


<p class="important"><strong>Note:</strong> If security is essential think about enabling CSRF cookies in <dfn>application/config/config.lc</dfn></p>



<p>&nbsp;</p>

<a href="#top">Top of Page</a>

<a name="handlerreference"></a>
<h2>Handler Reference</h2>


<h3>rigAuthLogin( <var>pIdentity</var>, <var>pPassword</var>, <var>pRemember</var> )</h3>

<p>Logs the user in.</p>

<p>The first parameter is a string to identify the user. Usually this is an email address or a username.<br />
	The second parameter is a password string.<br />
	The optional third parameter is a boolean which determines if the user wants to be remembered.</p>

<p>This function returns TRUE if the user was successfully logged in, otherwise it returns FALSE.</p>


<p>Example:</p>


<pre><code class="lang-livecodeserver">put FALSE into tRemember
# CHECK IF THERE IS A POST VARIABLE remember
put rigVarPost("remember") into tPostRemember
if tPostRemember &lt;> FALSE then
	# CHECK VALUE
	if tPostRemember[1] is 1 then
		put TRUE into tRemember
	end if
end if

get rigAuthLogin(rigVarPost("identity"), rigVarPost("password"), tRemember)
</code></pre>


<h3>rigAuthLogout</h3>

<p>Logs the user out.</p>


<p>Example:</p>

<pre><code>command logout
	# LOG USER OUT
	rigAuthLogout

	# GET AUTHENTICATION MESSAGES
	put rigAuthMessages() into tMessages

	rigSetSessFlashdata "message", tMessages
	
	# REDIRECT TO THE LOGIN PAGE
	rigRedirect "/auth/login"
end logout
</code></pre>



<h3>rigAuthRegister( <var>pUsername</var>, <var>pPassword</var>, <var>pEmail</var>, <var>pExtraData</var>, <var>pGroupname</var> )</h3>

<p>Register (create) a new user.</p>

<p>The first parameter is a username.<br />
	The second parameter is the user's password.<br />
	The third parameter is the user's email address.<br />
	The optional fourth parameter is an array containing additional data like first name, last name etc.<br />
	The optional fifth parameter is a numbered array used to define which groups the user belongs to.</p>

<p>This function returns a user ID if the user was successfully created, otherwise it returns FALSE.</p>


<pre><code># GET ACCOUNT DATA
if $_POST is an array then
	put toLower(rigVarPOST("first")) && toLower(rigVarPOST("last")) into tUserName
	put rigVarPOST("password") into tPassword
	put rigVarPOST("email") into tEmail
	
	put rigVarPOST("first") into tExtraData["firstName"]
	put rigVarPOST("last") into tExtraData["lastName"]
	put rigVarPOST("company") into tExtraData["company"]
	put rigVarPOST("phone") into tExtraData["phone"]
end if

put 2 into tGroups["1"]
put 3 into tGroups["2"]

put rigAuthRegister(tUserName, tPassword, tEmail, tExtraData, tGroups) into tRegistration
</code></pre>


<p class="important"><strong>Note:</strong> The default URI used for activating a new user is "auth/activate". This means you should provide a controller called "auth" including a handler called "activate" to process user activation. To specify your own URI segments you need to set the fourth parameter using the array key "activationLink" to "yourController/yourHandler". Of course, replace "yourController/yourHandler" with your URI segments.</p>



<h3>rigAuthUpdate( <var>pID</var>, <var>pData</var> )</h3>

<p>Update user data.</p>


<p>The first parameter is the user's ID.<br />
	The second parameter is an array containing the update data.</p>
	
	<p>This function returns TRUE if the user was successfully updated, otherwise it returns FALSE.</p>
	
	
<p>Example:</p>


<pre><code># GET USER ID
put rigFetchSegment(3) into tID

# CHECK POST ARRAY DATA
if $_POST is an array then
	# VALIDATE THE REQUEST
	if tID &lt;> rigVarPOST("hiddenUserID") then
		rigLogMessage "error", "This form post did not pass the security check.", TRUE
	end if
	
	put rigVarPost("phone") into tData["phone"]
	
	get rigAuthUpdate(tID, tData)
end if
</code></pre>




<h3>rigAuthDeleteUser( <var>pID</var> )</h3>

<p>Delete a user.</p>

<p>The parameter is the user's ID.</p>

<p>This function returns TRUE if the user was successfully deleted, otherwise it returns FALSE.</p>

<p>Example:</p>


<pre><code class="lang-livecodeserver"># GET USER ID
put rigFetchSegment(3) into tID

# CHECK THE USER LEVEL
if (rigAuthLoggedIn() is TRUE) and (rigAuthIsAdmin() is TRUE) then
	put rigAuthDeleteUser(tID) into tDeleted
end if
</code></pre>



<h3>rigAuthForgottenPassword( <var>pIdentity</var>, <var>pExtraData</var> )</h3>

<p>Resets the user's password by emailing the user a reset code.</p>

<p>The first parameter is a string which represents the user's identity as defined in the authentication.lc config file.</p>

<p>The optional second parameter is an array containing additional data. Currently the function processes solely one parameter array key which is called "passwordResetLink". This is the URI included with the forgotten password email used to reset a password.</p>

<p>This function returns TRUE if the user's password was reset successfully. It returns an array containing values for "identity" and "forgottenPasswordCode" in case sAuthenticationConf["sendEmails"] is set to FALSE.</p>


<pre><code class="lang-livecodeserver"># CALL THE rigAuthForgottenPassword FUNCTION TO SEND A RESET CODE TO THE USER
put rigAuthForgottenPassword(rigVarPOST("email")) into tResetPasswordSent

if tResetPasswordSent is TRUE then
	put rigAuthMessages() into tMessages
	
	rigSetSessFlashdata "message", tMessages
	rigRedirect "/auth/login"
else
	put rigAuthErrors() into tErrors
	
	rigSetSessFlashdata "message", tErrors
	rigRedirect "auth/forgotPassword"
end if
</code></pre>


<p class="important"><strong>Note:</strong> The default URI used to process forgotten password requests is "auth/resetPassword". This means you should provide a controller called "auth" including a handler called "resetPassword". To specify your own URI segments you need to set the second parameter using the array key "passwordResetLink" to "yourController/yourHandler". Of course, replace "yourController/yourHandler" with your URI segments.</p>



<h3>rigAuthLoggedIn()</h3>

<p>Check to see if a user is logged in.</p>

<p>This function returns TRUE if the user is logged in, otherwise it returns FALSE.</p>

<p>Example:</p>


<pre><code class="lang-livecodeserver"># CHECK IF USER IS LOGGED IN
if rigAuthLoggedIn() is FALSE then
	rigRedirect "/auth/login"
end if
</code></pre>



<h3>rigAuthIsAdmin( <var>pID</var> )</h3>


<p>Check to see if the currently logged in user is an administrator.</p>

<p>The optional parameter is the user's ID. If a user id is not passed the id of the currently logged in user will be used.</p>

<p>This function returns TRUE if the user is an admin, otherwise it returns FALSE.</p>

<p>Example:</p>


<pre><code class="lang-livecodeserver"># CHECK THE USER LEVEL
if rigAuthIsAdmin() is FALSE then
	# REDIRECT TO HOME PAGE BECAUSE USER IS NOT ALLOWED TO VIEW ADMIN CONTENT
	rigRedirect "/"
end if
</code></pre>


<h3>rigAuthInGroup( <var>pGroup</var>, <var>pID</var> )</h3>


<p>Check if user is a member of a specific group.</p>

<p>The first parameter is an integer (the group ID) or a string (the group name) or an array of strings and integers.<br />
	The second optional parameter is the user's ID. If a user id is not passed the id of the currently logged in user will be used.</p>

<p>This function returns TRUE if the user is a member of any given group, otherwise it returns FALSE.</p>

<p>Example:</p>


<pre><code># SINGLE GROUP (BY NAME)
put "other" into tGroup
if rigAuthInGroup(tGroup) then
	rigSetSessFlashdata "message", "You must be a member of group other to view this page."
	rigRedirect "/"
end if

# SINGLE GROUP (BY ID)
put 1 into tGroup
if rigAuthInGroup(tGroup) then
	rigSetSessFlashdata "message", "You must be a member of group 1 to view this page."
	rigRedirect "/"
end if

# MULTIBLE GROUPS (BY NAME)
put "members" into tGroups[1]
put "other" into tGroups[2]
if rigAuthInGroup(tGroups) then
	rigSetSessFlashdata "message", "You must be a member of group members or of group other to view this page."
	rigRedirect "/"
end if

# MULTIBLE GROUPS (BY ID)
put 2 into tGroups[1]
put 3 into tGroups[2]
if rigAuthInGroup(tGroups) then
	rigSetSessFlashdata "message", "You must be a member of group 2 or of group 3 to view this page."
	rigRedirect "/"
end if

# MULTIBLE GROUPS (BY NAME AND ID)
put 2 into tGroups[1]
put "other" into tGroups[2]
if rigAuthInGroup(tGroups) then
	rigSetSessFlashdata "message", "You must be a member of group 2 or of group other to view this page."
	rigRedirect "/"
end if
</code></pre>



<h3>rigAuthUsernameCheck( <var>pUsername</var> )</h3>


<p>Check if there is an entry in the DB regarding the username provided.</p>

<p>The parameter is the user's username.</p>

<p>This function returns TRUE if the user is registered, otherwise it returns FALSE.</p>

<p>Example:</p>

<pre><code># GET POST DATA
if $_POST is an array then
	put rigVarPOST("username") into tUsername
	put rigVarPOST("password") into tPassword
	put rigVarPOST("email") into tEmail

	put rigVarPOST("first") into tExtraData["firstName"]
	put rigVarPOST("last") into tExtraData["lastName"]
	put rigVarPOST("company") into tExtraData["company"]
	put rigVarPOST("phone") into tExtraData["phone"]
end if

put 2 into tGroups["1"]
put 3 into tGroups["2"]

if rigAuthUsernameCheck(tUsername) is FALSE then
	put rigAuthRegister(tUserName, tPassword, tEmail, tExtraData, tGroups) into tRegistration
end if
</code></pre>



<h3>rigAuthEmailCheck( <var>pEmail</var> )</h3>

<p>Check if there is an entry in the DB regarding the email address provided.</p>

<p>The parameter is the user's email address.</p>

<p>This function returns TRUE if the user is registered, otherwise it returns FALSE.</p>

<p>Example:</p>


<pre><code># GET POST DATA
if $_POST is an array then
	put rigVarPOST("username") into tUsername
	put rigVarPOST("password") into tPassword
	put rigVarPOST("email") into tEmail

	put rigVarPOST("first") into tExtraData["firstName"]
	put rigVarPOST("last") into tExtraData["lastName"]
	put rigVarPOST("company") into tExtraData["company"]
	put rigVarPOST("phone") into tExtraData["phone"]
end if

put 2 into tGroups["1"]
put 3 into tGroups["2"]

if rigAuthUsernameCheck(tEmail) is FALSE then
	put rigAuthRegister(tUserName, tPassword, tEmail, tExtraData, tGroups) into tRegistration
end if
</code></pre>



<h3>rigAuthIdentityCheck( <var>pIdentity</var> )</h3>


<p>Identity check for remembered users (auto-login).</p>

<p>The parameter is a string which represents the user's identity as defined in the authentication.lc config file.</p>

<p>This function returns TRUE if the user is registered, otherwise it returns FALSE.</p>

<p>Example:</p>

<pre><code># GET USER DATA
put rigAuthUser(tID) into tQueryResult
if (tQueryResult &lt;> FALSE) and (tQueryResult is an array) then
	put rigDbRow() into tUser
end if

put rigVarPOST("identity") into tData["identity"]
put rigVarPOST("first") into tData["firstName"]
put rigVarPOST("last") into tData["lastName"]

if (tData["identity"] is tUser["username"]) or (tData["identity"] is tUser["email"]) or (rigAuthIdentityCheck(tData["identity"]) is FALSE) then
	get rigAuthUpdate(tUser["id"], tData)
end if
</code></pre>


<h3>rigAuthIsMaxLoginAttemptsExceeded( <var>pIdentity</var> )</h3>

<p>If login attempt tracking is enabled, checks to see if the number of failed login attempts for this identity or ip address has been exceeded. Login attempt limits are not enforced in the library.</p>

<p>The parameter is a string which represents the user's identity as defined in the authentication.lc config file.</p>

<p>This function returns TRUE if maximum login attempts is exceeded, FALSE if not or if login attempts not tracked.</p>

<p>Example:</p>

<pre><code>put "rabit@revigniter.com" into tIdentity

if rigAuthIsMaxLoginAttemptsExceeded(tIdentity) is TRUE then
	rigSetSessFlashdata "message", "You have too many login attempts."
	rigRedirect "/"
end if
</code></pre>



<h3>rigAuthGetAttemptsNum( <var>pIdentity</var> )</h3>

<p>Get number of login attempts from a given ipAddress or identity.</p>

<p>The parameter is a string which represents the user's identity as defined in the authentication.lc config file.</p>

<p>This function returns the number of failed login attempts for this identity or ip address.</p>

<p>Example:</p>

<pre><code>put "rabit@revigniter.com" into tIdentity

put rigAuthGetAttemptsNum(tIdentity) into tNumAttempts
</code></pre>



<h3>rigAuthIncreaseLoginAttempts( <var>pIdentity</var> )</h3>

<p>If login attempt tracking is enabled, records another failed login attempt for this identity or ip address. This function is automatically called by the rigAuthLogin() function in case the login failed.</p>

<p>The parameter is a string which represents the user's identity as defined in the authentication.lc config file.</p>

<p>This function returns TRUE if increasing the number was successful, otherwise it returns FALSE.</p>



<h3>rigAuthClearLoginAttempts( <var>pIdentity</var>, <var>pExpirePeriod</var> )</h3>

<p>Clears all failed login attempt records for this identity or this ip address. This function is automatically called by the rigAuthLogin() function if the login succeeded.</p>

<p>The first parameter is a string which represents the user's identity as defined in the authentication.lc config file.<br />
	The second optional parameter is the number of seconds after which all the entries of login attempts should be cleared. If set to empty, a value of 86400 ( 1 day) is used.</p>
	

<h3>rigAuthUser( <var>pID</var> )</h3>

<p>Get a particular user.</p>

<p>The optional parameter is the user's id (integer). If a user id is not passed the id of the currently logged in user will be used.</p>

<p>This function returns FALSE in case there is no user data with the given id, otherwise it returns the result of the database query as an array.</p>

<p>Example:</p>

<pre><code class="lang-livecodeserver"># GET USER DATA
put rigAuthUser() into tQueryResult
if (tQueryResult &lt;> FALSE) and (tQueryResult is an array) then
	put rigDbRow() into tUser
end if

# NOW tUser CONTAINS THE USER RECORD
# tUser["id"], tUser["ipAddress"], tUser["username"], tUser["password"], tUser["email"],
# tUser["activationCode"], tUser["forgottenPasswordCode"], tUser["forgottenPasswordTime"],
# tUser["rememberCode"], tUser["createdOn"], tUser["lastLogin"], tUser["active"],
# tUser["firstName"], tUser["lastName"], tUser["company"], tUser["phone"]
</code></pre>



<h3>rigAuthUsers( <var>pGroupIDs</var> )</h3>

<p>Get the users.</p>

<p>The optional parameter is an array of group IDs or a single ID.</p>

<p>This function returns FALSE in case there is no user data with the given group id, otherwise it returns the users who are members of the groups specified by the parameter or all users if the parameter is empty.</p>

<p>Example:</p>

<pre><code># GET ALL ADMINS
put rigAuthUsers(1) into tAdmins

# tAdmins IS NOW A MULTIDIMENSIONAL NUMBERED ARRAY
# CONTAINING ALL USERS WHO ARE A MEMBER OF THE ADMIN GROUP

put 0 into tCounter
repeat for each key tRecord in tAdmins
	add 1 to tCounter

	put tAdmins[tCounter] into tUser

	put empty into tRowData

	put "Username:" && tUser[3] & ", " after tRowData
	put "First Name:" && tUser[13] & ", " after tRowData
	put "Last Name:" && tUser[14] & ", " after tRowData
	delete char -2  to -1 of tRowData

	put tRowData & "&lt;br />" & return after tResultData
end repeat
</code></pre>


<h3>rigAuthGroup( <var>pID</var> )</h3>

<p>Get a specific group.</p>

<p>The parameter is a group id.</p>

<p>This function returns FALSE in case there is no group data with the given id, otherwise it returns a group, the result of the database query as a numbered array.</p>

<p>Example:</p>


<pre><code># GET MEMBERS GROUP
put rigAuthGroup(2) into tGroup

# tGroup IS NOW A MULTIDIMENSIONAL NUMBERED ARRAY
# CONTAINING 1 GROUP RECORD

put tGroup[1] into tGroupRow

put "Group ID:" && tGroupRow[1] & ", " after tRowData
put "Group Name:" && tGroupRow[2] & ", " after tRowData
put "Group Description:" && tGroupRow[3] & ", " after tRowData
delete char -2  to -1 of tRowData
</code></pre>



<h3>rigAuthGroups()</h3>

<p>Get the groups.</p>

<p>This function returns FALSE in case there are no groups, otherwise it returns all groups, the result of the database query as a numbered array.</p>

<p>Example:</p>


<pre><code># GET GROUPS
put rigAuthGroups() into tGroups

# tGroups IS NOW A MULTIDIMENSIONAL NUMBERED ARRAY
# CONTAINING ALL GROUP RECORDS

put 0 into tCounter
repeat for each key tRecord in tGroups
	add 1 to tCounter

	put tGroups[tCounter] into tGroupRow

	put empty into tRowData

	put "Group ID:" && tGroupRow[1] & ", " after tRowData
	put "Group Name:" && tGroupRow[2] & ", " after tRowData
	put "Group Description:" && tGroupRow[3] & ", " after tRowData
	delete char -2  to -1 of tRowData

	put tRowData & "&lt;br />" & return after tResultData
end repeat
</code></pre>




<h3>rigAuthUserGroups( <var>pID</var> )</h3>

<p>Get groups of a specific user.</p>

<p>The optional parameter is a user's id.</p>

<p>This function returns FALSE in case the user with the given id is not a member of any group, otherwise it returns all groups the user is a member of, the result of the database query as a numbered array.</p>

<p>Example:</p>


<pre><code># GET ALL GROUPS THE ADMIN IS A MEMBER OF
put rigAuthUserGroups(1) into tUserGroups

# tUserGroups IS NOW A MULTIDIMENSIONAL NUMBERED ARRAY
# CONTAINING GROUP RECORDS

put 0 into tCounter
repeat for each key tRecord in tUserGroups
	add 1 to tCounter

	put tUserGroups[tCounter] into tGroupRow

	put empty into tRowData

	put tGroupRow[2] into tRowData

	put tRowData & "," after tResultData
end repeat
if char -1 of tResultData is "," then
	delete char -1 of tResultData
end if
</code></pre>



<h3>rigAuthAddToGroup <var>pGroupID</var>, <var>pUserID</var></h3>

<p>Add user to a group.</p>

<p>The first parameter is an integer, the ID of the group a user should be added to.<br />
	The second parameter is the user's ID.</p>
	
	<p>Example:</p>


<pre><code class="lang-livecodeserver"># GET USER ID
put rigFetchSegment(3) into tUserID

# CHECK POST ARRAY DATA
if $_POST is an array then
	# VALIDATE THE REQUEST
	if tUserID &lt;> rigVarPOST("hiddenUserID") then
		rigLogMessage "error", "This form post did not pass the security check.", TRUE
	else
	
		rigAuthAddToGroup 3, tUserID
	end if
end if
</code></pre>




<h3>rigAuthRemoveFromGroup( <var>pGroupIDs</var>, <var>pUserID</var> )</h3>

<p>Remove user from group.</p>

<p>The first parameter is an array of group IDs or a single group ID.<br />
	The second parameter is the user's id.</p>

<p>This function returns TRUE if the user was successfully removed from the group(s), otherwise it returns FALSE.</p>

<p>Example:</p>


<pre><code># PASS AN ARRAY OF GROUP ID's AND A USER ID
put 2 into tGroups[1]
put 5 into tGroups[2]
put 7 into tGroups[3]
get rigAuthRemoveFromGroup(tGroupIDs, tUserID)

# PASS A SINGLE ID AND USER ID
get rigAuthRemoveFromGroup(tGroupID, tUserID)

# PASS AN EMPTY GROUP ID TO REMOVE THE USER FROM ALL GROUPS
get rigAuthRemoveFromGroup(, tUserID)
</code></pre>




<h3>rigAuthMessages( <var>pPrefix</var>, <var>pSuffix</var> )</h3>

<p>Get authentication messages.</p>

<p>The first optional parameter is a starting delimiter for authentication related messages. If none is specified the delimiter as defined in <dfn>application/config/authentication.lc</dfn> is used.<br />
	The second optional parameter is a ending delimiter for authentication related messages. If none is specified the delimiter as defined in <dfn>application/config/authentication.lc</dfn> is used.</p>
	
<p>This function returns all authentication messages as a string.</p>

<p>Example:</p>


<pre><code># IF REGISTRATION WAS SUCCESSFUL REDIRECT TO AUTHENTICATION PAGE
if tRegistration &lt;> FALSE then		
	put rigAuthMessages() into tMessages
	rigSetSessFlashdata "message", tMessages
	rigRedirect "/auth"
end if
</code></pre>




<h3>rigAuthSetMessageDelimiters <var>pStartDelimiter</var>, <var>pEndDelimiter</var></h3>

<p>Set the message delimiters.</p>

<p>The first parameter is the staring delimiter.<br />
	The second parameter is the ending delimiter.</p>
	
<p>Example:</p>


<pre><code># IF REGISTRATION WAS SUCCESSFUL REDIRECT TO AUTHENTICATION PAGE
if tRegistration &lt;> FALSE then
	rigAuthSetMessageDelimiters "&lt;p>&lt;strong>", "&lt;/strong>&lt;/p>"
		
	put rigAuthMessages() into tMessages
	rigSetSessFlashdata "message", tMessages
	rigRedirect "/auth"
end if
</code></pre>



<h3>rigAuthErrors( <var>pPrefix</var>, <var>pSuffix</var> )</h3>

<p>Get authentication errors.</p>

<p>The optional first parameter is the staring delimiter. If none is specified the delimiter as defined in <dfn>application/config/authentication.lc</dfn> is used.<br />
	The optional second parameter is the ending delimiter. If none is specified the delimiter as defined in <dfn>application/config/authentication.lc</dfn> is used.</p>
	
<p>This function returns authentication errors as a string.</p>


<pre><code>get rigAuthLogin(rigVarPost("identity"), rigVarPost("password"), tRemember)

# IF LOGIN IS UNSUCCESSFUL REDIRECT BACK TO THE LOGIN PAGE
if it is FALSE then
	put rigAuthErrors() into tErrors
	rigSetSessFlashdata "message", tErrors
	rigRedirect "auth/login"
end if
</code></pre>



<h3>rigAuthSetErrorDelimiters <var>pStartDelimiter</var>, <var>pEndDelimiter</var></h3>

<p>Set the message delimiters.</p>

<p>The first parameter is the staring delimiter.<br />
	The second parameter is the ending delimiter.</p>
	
<p>Example:</p>


<pre><code>rigAuthSetErrorDelimiters "&lt;p>&lt;strong>", "&lt;/strong>&lt;/p>"
	
get rigAuthLogin(rigVarPost("identity"), rigVarPost("password"), tRemember)

# IF LOGIN IS UNSUCCESSFUL REDIRECT BACK TO THE LOGIN PAGE
if it is FALSE then
	put rigAuthErrors() into tErrors
	rigSetSessFlashdata "message", tErrors
	rigRedirect "auth/login"
end if
</code></pre>




<h3>rigAuthFetchConfigItem( <var>pItem</var>, <var>pIndex</var> )</h3>

<p>Get a value from the configuration settings array.</p>

<p>The first parameter is the array key.<br />
	The optional second parameter is the second array key.</p>

<p>Example:</p>


<pre><code># GET MIN / MAX PASSWORD LENGTH
put rigAuthFetchConfigItem("minLengthPassword") into tMinPasswordLength
put rigAuthFetchConfigItem("maxLengthPassword") into tMaxPasswordLength

# CHANGE PASSWORD
put rigAuthFetchConfigItem("identityColumn") into tIdentityCol
put tUser[tIdentityCol] into tIdentity
put rigAuthResetPassword(tIdentity, rigVarPOST("newPassword")) into tChange

# GET THE NAME OF THE USERS TABLE
put rigAuthFetchConfigItem("tables", "users") into tUsersTableName
</code></pre>




<h3>rigAuthActivate( <var>pID</var>, <var>pCode</var> )</h3>

<p>Activate a user account.</p>

<p>The first parameter is the user's id.<br />
	The second optional parameter is the activation code.</p>
	
<p>This function returns TRUE in case user activation was successful, otherwise it returns FALSE.</p>
	
<p>Example:</p>


<pre><code># GET USER ID AND ACTIVATION CODE
put rigFetchSegment(3) into tID
put rigFetchSegment(4) into tCode
put FALSE into tActivation

# ACTIVATE USER
if tCode &lt;> FALSE then
	put rigAuthActivate(tID, tCode) into tActivation
	
else if rigAuthIsAdmin() is TRUE then
	put rigAuthActivate(tID) into tActivation
end if		
</code></pre>



<h3>rigAuthDeactivate( <var>pID</var> )</h3>

<p>Deactivate a user.</p>

<p>The parameter is the user's id.</p>

<p>This function returns TRUE in case user deactivation was successful, otherwise it returns FALSE.</p>

<p>Example:</p>



<pre><code>if (rigAuthLoggedIn() is TRUE) and (rigAuthIsAdmin() is TRUE) then
	put rigAuthDeactivate(tID) into tDeactivated
end if
</code></pre>



<h3>rigAuthForgottenPasswordCheck( <var>pCode</var> )</h3>

<p>Check the code used to reset a password.</p>

<p>The parameter is the "forgotten password" code.</p>

<p>This function returns user data as an array in case the provided code was checked successfully, otherwise it returns FALSE.</p>

<p>Example:</p>


<pre><code>put rigFetchSegment(3) into tForgottenPasswordCode

if tForgottenPasswordCode is FALSE then
	rigShow404
end if

put rigAuthForgottenPasswordCheck(tForgottenPasswordCode) into tUser
</code></pre>



<h3>rigAuthClearForgottenPasswordCode( <var>pCode</var> )</h3>

<p>Delete the forgotten password code.</p>

<p>The parameter is the code used to reset a password.</p>

<p>This function returns TRUE in case the code used to reset a password was deleted successfully, otherwise it returns FALSE.</p>


<p>Example:</p>


<pre><code>if tUser["id"] &lt;> rigVarPOST("hiddenUserID") then
	get rigAuthClearForgottenPasswordCode(tForgottenPasswordCode)
	rigLogMessage "error", "This reset password form post did not pass the security check.", TRUE
end if
</code></pre>



<h3>rigAuthResetPassword( <var>pIdentity</var>, <var>pNewPassword</var> )</h3>

<p>Replace password with a new one.</p>

<p>The first parameter is the user's email address or whatever is used to identify a user.<br />
	The second parameter is the new password.</p>
	
<p>This function returns TRUE in case the old password was replaced successfully, otherwise it returns FALSE.</p>

<p>Example:</p>


<pre><code># RESET THE PASSWORD
put rigAuthFetchConfigItem("identityColumn") into tIdentityCol
put tUser[tIdentityCol] into tIdentity

put rigAuthResetPassword(tIdentity, rigVarPOST("newPassword")) into tChange
</code></pre>



<h3>rigAuthChangePassword( <var>pIdentity</var>, <var>pOldPassword</var>, <var>pNewPassword</var> )</h3>

<p>Validate and change a password.</p>


<p>The first parameter is the user's email address or whatever is used to identify a user.<br />
	The second parameter is the old password.<br />
	The third parameter is the new password.</p>
	
<p>This function returns TRUE in case the password was changed successfully, otherwise it returns FALSE.</p>	


<p>Example:</p>


<pre><code>put rigAuthFetchConfigItem("identityColumn") into tIdentityCol
put rigSessUserdata(tIdentityCol) into tIdentity

put rigAuthChangePassword(tIdentity, rigVarPOST("oldPassword"), rigVarPOST("newPassword")) into tChange
</code></pre>





<a href="#top">Top of Page</a>


<p>&nbsp;</p>

<a name="hooks"></a>
<h1>Authentication Hooks</h1>

<p>Authentication hooks work similar like those extending the framework core. So, if you want to run your own scripts at a particular stage of the library's execution process, the hooks feature is your friend.</p>


<h2>Enabling Authentication Hooks</h2>

<p>The authentication hooks feature can be enabled/disabled by setting the following item in the <dfn>application/config/authentication.lc</dfn> file:</p>


<pre><code>put TRUE into sAuthenticationConf["enableAuthenticationHooks"]</code></pre>


<h2>Defining an Authentication Hook</h2>

<p>Authentication hooks are defined in the <dfn>application/config/authenticationHooks.lc</dfn> file.  Each hook is specified as an array with this prototype:</p>


<pre><code># IN THIS CASE THE HOOK POINT IS RIGHT BEFORE UPDATING USER DATA
put "myHandler" into sAuthenticationHooks["authPreUpdateUser"]["handler"]
put "myScript.lc" into sAuthenticationHooks["authPreUpdateUser"]["filename"]
put "hooks/authenticationHooks" into sAuthenticationHooks["authPreUpdateUser"]["filepath"]
put "beer" into sAuthenticationHooks["authPreUpdateUser"]["params"][1]
put "wine" into sAuthenticationHooks["authPreUpdateUser"]["params"][2]
put "snacks" into sAuthenticationHooks["authPreUpdateUser"]["params"][3]
</code></pre>

<p><strong>Notes:</strong><br />The array index correlates to the name of the particular hook point you want to
use.  In the above example the hook point is <kbd>authPreUpdateUser</kbd>. A list of hook points is found below.
The following items should be defined in your associative hook array:</p>

<ul>
<li><strong>handler</strong>&nbsp; The handler name you wish to call. If you call a function, append a pair of brackets to the name like: "myHandler()".</li>
<li><strong>filename</strong>&nbsp; The file name containing your handler.</li>
<li><strong>filepath</strong>&nbsp; The name of the directory containing your script.  Note: Your script must be located in a directory INSIDE your <kbd>application</kbd> folder, so the file path is relative to that folder.  For example, if your script is located in <dfn>application/hooks/authenticationHooks</dfn>, you will simply use <samp>hooks/authenticationHooks</samp> as your filepath.  If your script is located in <dfn>application/hooks/authenticationUtilities</dfn> you will use <samp>hooks/authenticationUtilities</samp> as your filepath. No trailing slash.</li>
<li><strong>params</strong>&nbsp; Any parameters you wish to pass to your script. This item is optional.</li>
</ul>


<p class="important"><strong>Note:</strong> The handlers in your hook files have access to all variables declared in the Authentication.lc library.</dfn></p>


<h2>Multiple Calls to the Same Hook</h2>

<p>If you want to use the same hook point with more then one script, simply make your array declaration multidimensional, like this:</p>

<pre><code>put "myHandler" into sAuthenticationHooks["authPreUpdateUser"][1]["handler"]
put "myScript.lc" into sAuthenticationHooks["authPreUpdateUser"][1]["filename"]
put "hooks/authenticationHooks" into sAuthenticationHooks["authPreUpdateUser"][1]["filepath"]
put "beer" into sAuthenticationHooks["authPreUpdateUser"][1]["params"][1]
put "wine" into sAuthenticationHooks["authPreUpdateUser"][1]["params"][2]
put "snacks" into sAuthenticationHooks["authPreUpdateUser"][1]["params"][3]

put "myOtherHandler" into sAuthenticationHooks["authPreUpdateUser"][2]["handler"]
put "myOtherScript.lc" into sAuthenticationHooks["authPreUpdateUser"][2]["filename"]
put "hooks/authenticationHooks" into sAuthenticationHooks["authPreUpdateUser"][2]["filepath"]
put "red" into sAuthenticationHooks["authPreUpdateUser"][2]["params"][1]
put "yellow" into sAuthenticationHooks["authPreUpdateUser"][2]["params"][2]
put "blue" into sAuthenticationHooks["authPreUpdateUser"][2]["params"][3]
</code></pre>

<p>This permits you to have the same hook point with multiple scripts.  The order you define your array will be the execution order.</p>


<h2>Authentication hook Points</h2>

<p>The following is a list of available hook points.</p>


<ul>
	<li><strong>authLibraryInitialized</strong><br />
		Called very early right after the library has been initialized.</li>
	<li><strong>authPreActivate</strong><br />
		Called before a user is activated.</li>
	<li><strong>authPostActivate</strong><br />
		Called after user activation.</li>
	<li><strong>authPostActivateUnsuccessful</strong><br />
		Called after user activation failed.</li>
	<li><strong>authPostActivateSuccessful</strong><br />
		Called after successfully activating a user.</li>
	<li><strong>authExtraWhere</strong><br />
		Called before any database query.</li>
	<li><strong>authDeactivate</strong><br />
		Called before deactivating a user.</li>
	<li><strong>authCheckLoggedIn</strong><br />
		Called before checking a user's logged in state.</li>
	<li><strong>authIsAdmin</strong><br />
		Called before checking if a particular user is a member of the admin group.</li>
	<li><strong>authInGroup</strong><br />
		Called before checking if a particular user is a member of a particular group.</li>
	<li><strong>authUsers</strong><br />
		Called before getting all users.</li>
	<li><strong>authUser</strong><br />
		Called before getting a particular users.</li>
	<li><strong>authUserGroups</strong><br />
		Called before getting the groups a particular user is a member of.</li>
	<li><strong>authPostLogin</strong><br />
		Called after logging in.</li>
	<li><strong>authPostLoginUnsuccessful</strong><br />
		Called after logging in failed.</li>
	<li><strong>authPostLoginSuccessful</strong><br />
		Called after successfully logging in.</li>
	<li><strong>authLogout</strong><br />
		Called before logging out.</li>
	<li><strong>authPreAccountCreation</strong><br />
		Called before a new user is created.</li>
	<li><strong>authPostAccountCreation</strong><br />
		Called after creating a new user.</li>
	<li><strong>authPostAccountCreationSuccessful</strong><br />
		Called after successfully creating a new user.</li>	
	<li><strong>authPostAccountCreationUnsuccessful</strong><br />
		Called after creating a new user failed.</li>
	<li><strong>authPActivationEmailSuccessful</strong><br />
		Called after sending an activation email or after returning activation data in case email activation is turned off.</li>
	<li><strong>authPActivationEmailUnsuccessful</strong><br />
		Called after sending an activation email failed.</li>
	<li><strong>authPreRegister</strong><br />
		Called registering a user.</li>
	<li><strong>authExtraSet</strong><br />
		Called before inserting user data while registering a user.</li>
	<li><strong>authPostRegister</strong><br />
		Called after registering a user.</li>
	<li><strong>authUpdateLastLogin</strong><br />
		Called before updating the las login time.</li>
	<li><strong>authPreRememberUser</strong><br />
		Called before turning on auto login.</li>
	<li><strong>authPostRememberUser</strong><br />
		Called after turning on auto login.</li>
	<li><strong>authRememberUserSuccessful</strong><br />
		Called after successfully turning on auto login.</li>
	<li><strong>authRememberUserUnsuccessful</strong><br />
		Called after turning on auto login failed.</li>	
	<li><strong>authPreLoginRememberedUser</strong><br />
		Called before auto login of remembered users.</li>
	<li><strong>authPostLoginRememberedUser</strong><br />
		Called after auto login of remembered users.</li>
	<li><strong>authLoginRememberedUserUnsuccessful</strong><br />
		Called after auto login failed.</li>
	<li><strong>authPostLoginRememberedUserSuccessful</strong><br />
		Called after a successfull auto login of remembered users.</li>
	<li><strong>authIdentityCheck</strong><br />
		Called before the identity check for remembered users.</li>
	<li><strong>authEmailCheck</strong><br />
		Called before checking if a particular email address exists in the database.</li>
	<li><strong>authUsernameCheck</strong><br />
		Called before checking if a particular username exists in the database.</li>
	<li><strong>authAddToGroup</strong><br />
		Called before adding a user to a group.</li>
	<li><strong>authGroups</strong><br />
		Called before getting all groups.</li>
	<li><strong>authGroup</strong><br />
		Called before getting a particular group.</li>
	<li><strong>authPostFrogottenPassword</strong><br />
		Called after inserting a forgotten password key in the database.</li>
	<li><strong>authPostFrogottenPasswordUnsuccessful</strong><br />
		Called after inserting a forgotten password key in the database failed.</li>
	<li><strong>authPostFrogottenPasswordSuccessful</strong><br />
		Called after successfully inserting a forgotten password key in the database.</li>
	<li><strong>authPreChangePassword</strong><br />
		Called before replacing a password with a new one.</li>
	<li><strong>authPostChangePassword</strong><br />
		Called after replacing a password with a new one.</li>
	<li><strong>authPostChangePasswordUnsuccessful</strong><br />
		Called after replacing a password with a new one failed.</li>
	<li><strong>authPostChangePasswordSuccessful</strong><br />
		Called after successfully replacing a password with a new one.</li>
	<li><strong>authPreChangePassword</strong><br />
		Called before processing a user's change password request.</li>
	<li><strong>authPostChangePassword</strong><br />
		Called after processing a user's change password request.</li>
	<li><strong>authPostChangePasswordUnsuccessful</strong><br />
		Called after processing a user's change password request failed.</li>
	<li><strong>authPostChangePasswordSuccessful</strong><br />
		Called after successfully processing a user's change password request.</li>
	<li><strong>authPreUpdateUser</strong><br />
		Called before updating user data.</li>
	<li><strong>authPostUpdateUser</strong><br />
		Called after updating user data.</li>
	<li><strong>authPostUpdateUserUnsuccessful</strong><br />
		Called after updating user data failed.</li>
	<li><strong>authPostUpdateUserSuccessful</strong><br />
		Called after successfully updating user data.</li>
	<li><strong>authPreDeleteUser</strong><br />
		Called before deleting a user.</li>
	<li><strong>authPostDeleteUser</strong><br />
		Called after deleting a user.</li>
	<li><strong>authPostDeleteUserUnsuccessful</strong><br />
		Called after deleting a user failed.</li>
	<li><strong>authPostDeleteUserSuccessful</strong><br />
		Called after successfully deleting a user.</li>
	<li><strong>authRemoveFromGroup</strong><br />
		Called before removing a user from a group.</li>
</ul>


<h2>Hook Example Using authPostLoginSuccessful and authLogout as Hook Point</h2>

<p>The following is hardly a real world example but at least it illustrates the concept. Let's say you want to log whenever an admin logs in and out. This can easily be accomplished with the help of authentication hooks.</p>


<p>Create a hooks file called authLog.lc. In it, place the code below and save it to your system/application/hooks/authenticationHooks/ folder.</p>


<pre><code>&lt;?rev
put gBASEPATH into gBASEPATH

if gBASEPATH is "gBASEPATH" then
	put "No direct script access allowed."
	exit to top
end if



/*----------------------------------------------------------------------
--| COMMAND rigLogAdminLogin
--|
--| Author: rabit
--| Version:  1.0
--| Created: 22-02-2013
--| Last Mod: 22-02-2013
--| Requires: rigLogMessage
--|
--| Summary: Log admin login.
--| 
--| Format:  rigLogAdminLogin	
--|
--| Parameters: --
--|
--| Return: empty
----------------------------------------------------------------------*/

command rigLogAdminLogin
	# sAuthUser IS A VARIABLE DECLARED IN THE AUTHENTICATION LIBRARY
	if sAuthUser["username"] is "admin" then
		rigLogMessage "info", "Admin logged in."
	end if
end rigLogAdminLogin





/*----------------------------------------------------------------------
--| COMMAND rigLogAdminLogout
--|
--| Author: rabit
--| Version:  1.0
--| Created: 22-02-2013
--| Last Mod: 22-02-2013
--| Requires: rigSessUserdata(), rigLogMessage
--|
--| Summary: Log admin logout.
--| 
--| Format:  rigLogAdminLogout	
--|
--| Parameters: --
--|
--| Return: empty
----------------------------------------------------------------------*/

command rigLogAdminLogout
		if rigSessUserdata("username") is "admin" then
			rigLogMessage "info", "Admin logged out."
		end if
end rigLogAdminLogout




--| END OF authLog.lc
--| Location:  ./system/application/hooks/authenticationHooks/authLog.lc
----------------------------------------------------------------------
</code></pre>



<p>The next step is to add the following lines to your authenticationHooks.lc file in <dfn>system/application/config</dfn>:</p>


<pre><code>put "rigLogAdminLogin" into sAuthenticationHooks["authPostLoginSuccessful"]["handler"]
put "authLog.lc" into sAuthenticationHooks["authPostLoginSuccessful"]["filename"]
put "hooks/authenticationHooks" into sAuthenticationHooks["authPostLoginSuccessful"]["filepath"]


put "rigLogAdminLogout" into sAuthenticationHooks["authLogout"]["handler"]
put "authLog.lc" into sAuthenticationHooks["authLogout"]["filename"]
put "hooks/authenticationHooks" into sAuthenticationHooks["authLogout"]["filepath"]
</code></pre>



<p>Now enable authentication hooks in <dfn>application/config/authentication.lc</dfn> and your are done.</p>




<a href="#top">Top of Page</a>


<p>&nbsp;</p>

<a name="tables"></a>
<h1>Authentication Tables</h1>

<p>In order to use revIgniter's authentication library you need 4 database tables.</p>

<p class="important"><strong>Note:</strong> The default admin user has the following credentials. Identity: admin@admin.com Password: revigniter. This password works as long as you use the default authentication encryption settings.</p>

<p><strong>The MySQL version:</strong></p>


<pre><code># Table structure for groups
# ------------------------------------------------------------

CREATE TABLE `groups` (
  `id` mediumint(8) unsigned NOT NULL auto_increment,
  `name` varchar(20) NOT NULL,
  `description` varchar(100) NOT NULL,
  PRIMARY KEY  (`id`)
) ENGINE=MyISAM DEFAULT CHARSET=latin1;



# Records of groups
# ------------------------------------------------------------

LOCK TABLES `groups` WRITE;

INSERT INTO `groups` (`id`, `name`, `description`)
VALUES
	(1,'admin','Administrator'),
	(2,'members','General User'),
	(3,'other','Other User');

UNLOCK TABLES;



# Table structure for login_attempts
# ------------------------------------------------------------

CREATE TABLE `login_attempts` (
  `id` mediumint(8) unsigned NOT NULL auto_increment,
  `ipAddress` varchar(16) NOT NULL,
  `login` varchar(100) NOT NULL,
  `time` int(11) unsigned default NULL,
  PRIMARY KEY  (`id`)
) ENGINE=MyISAM DEFAULT CHARSET=latin1;



# Table structure for users
# ------------------------------------------------------------

CREATE TABLE `users` (
  `id` mediumint(8) unsigned NOT NULL auto_increment,
  `ipAddress` varchar(16) character set utf8 collate utf8_unicode_ci NOT NULL,
  `username` varchar(100) character set utf8 collate utf8_unicode_ci NOT NULL,
  `password` tinytext character set utf8 collate utf8_unicode_ci NOT NULL,
  `email` varchar(100) character set utf8 collate utf8_unicode_ci NOT NULL,
  `activationCode` varchar(40) character set utf8 collate utf8_unicode_ci default NULL,
  `forgottenPasswordCode` varchar(40) character set utf8 collate utf8_unicode_ci default NULL,
  `forgottenPasswordTime` int(11) unsigned NOT NULL default '0',
  `rememberCode` varchar(40) character set utf8 collate utf8_unicode_ci default NULL,
  `createdOn` int(11) unsigned NOT NULL,
  `lastLogin` int(11) unsigned default NULL,
  `active` tinyint(1) unsigned default NULL,
  `firstName` varchar(50) character set utf8 collate utf8_unicode_ci default NULL,
  `lastName` varchar(50) character set utf8 collate utf8_unicode_ci default NULL,
  `company` varchar(100) character set utf8 collate utf8_unicode_ci default NULL,
  `phone` varchar(20) character set utf8 collate utf8_unicode_ci default NULL,
  PRIMARY KEY  (`id`)
) ENGINE=MyISAM DEFAULT CHARSET=latin1;



# Records of users
# ------------------------------------------------------------

LOCK TABLES `users` WRITE;

INSERT INTO `users` (`id`, `ipAddress`, `username`, `password`, `email`, 
`activationCode`, `forgottenPasswordCode`, `forgottenPasswordTime`, 
`rememberCode`, `createdOn`, `lastLogin`, `active`, `firstName`, 
`lastName`, `company`, `phone`)
VALUES (1,'127.0.0.1','admin','U2FsdGVkX1/HRLAXNh9nUG3k6qJU5OeE9rACvDBL5WLsD7vPB9S44w==',
'admin@admin.com','',NULL,0,NULL,1361712014,1361732014,1,'Admin','Istrator','ADMIN','0');

UNLOCK TABLES;


# Table structure for users_groups
# ------------------------------------------------------------

DROP TABLE IF EXISTS `users_groups`;

CREATE TABLE `users_groups` (
  `id` mediumint(8) unsigned NOT NULL auto_increment,
  `userId` mediumint(8) unsigned NOT NULL,
  `groupId` mediumint(8) unsigned NOT NULL,
  PRIMARY KEY  (`id`)
) ENGINE=MyISAM DEFAULT CHARSET=latin1;



# Records of users_groups
# ------------------------------------------------------------

LOCK TABLES `users_groups` WRITE;

INSERT INTO `users_groups` (`id`, `userId`, `groupId`)
VALUES
	(1,1,1),
	(2,1,2);

UNLOCK TABLES;
</code></pre>


<p><strong>The PostgreSQL version:</strong></p>


<pre><code># Table structure for users
# ------------------------------------------------------------

CREATE TABLE "users" (
    "id" SERIAL NOT NULL,
    "ipAddress" varchar(16) NOT NULL,
    "username" varchar(100) NOT NULL,
    "password" text NOT NULL,
    "email" varchar(100) NOT NULL,
    "activationCode" varchar(40),
    "forgottenPasswordCode" varchar(40),
    "forgottenPasswordTime" int,
    "rememberCode" varchar(40),
    "createdOn" int NOT NULL,
    "lastLogin" int,
    "active" int4,
    "firstName" varchar(50),
    "lastName" varchar(50),
    "company" varchar(100),
    "phone" varchar(20),
  PRIMARY KEY("id"),
  CONSTRAINT "check_id" CHECK(id >= 0),
  CONSTRAINT "check_active" CHECK(active >= 0)
);



# Table structure for groups
# ------------------------------------------------------------

CREATE TABLE "groups" (
    "id" SERIAL NOT NULL,
    "name" varchar(20) NOT NULL,
    "description" varchar(100) NOT NULL,
  PRIMARY KEY("id"),
  CONSTRAINT "check_id" CHECK(id >= 0)
);



# Table structure for users_groups
# ------------------------------------------------------------

CREATE TABLE "users_groups" (
    "id" SERIAL NOT NULL,
    "userId" integer NOT NULL REFERENCES "users" ON UPDATE CASCADE ON DELETE CASCADE,
    "groupId" integer NOT NULL REFERENCES "groups" ON UPDATE CASCADE ON DELETE RESTRICT,
  PRIMARY KEY("id"),
  CONSTRAINT "users_groups_check_id" CHECK(id >= 0),
  CONSTRAINT "users_groups_check_userId" CHECK("userId" >= 0),
  CONSTRAINT "users_groups_check_groupId" CHECK("groupId" >= 0)
);



# Records of groups
# ------------------------------------------------------------

INSERT INTO "groups" (id, name, description)
SELECT 1,'admin','Administrator'
UNION SELECT
2,'members','General User'
UNION SELECT
3,'other','Other User';



# Records of users
# ------------------------------------------------------------

INSERT INTO "users" ("ipAddress", username, password, email, 
"activationCode", "forgottenPasswordCode", "forgottenPasswordTime", 
"rememberCode", "createdOn", "lastLogin", active, "firstName", 
"lastName", company, phone) VALUES 
('127.0.0.1','admin','U2FsdGVkX1/HRLAXNh9nUG3k6qJU5OeE9rACvDBL5WLsD7vPB9S44w==',
'admin@admin.com','',NULL,'0',NULL,'1361712014','1361732014','1',
'Admin','Istrator','ADMIN','0');



# Records of users_groups
# ------------------------------------------------------------

INSERT INTO "users_groups" ("userId", "groupId")
SELECT 1, 1
UNION SELECT
1, 2;



# Table structure for login_attempts
# ------------------------------------------------------------

CREATE TABLE "login_attempts" (
    "id" SERIAL NOT NULL,
    "ipAddress" varchar(16) NOT NULL,
    "login" varchar(100) NOT NULL,
    "time" int,
  PRIMARY KEY("id"),
  CONSTRAINT "check_id" CHECK(id >= 0)
);
</code></pre>


<p><strong>The SQLite version:</strong></p>


<pre><code># Table structure for users
# ------------------------------------------------------------

CREATE TABLE "users" (
	 "id" INTEGER NOT NULL PRIMARY KEY AUTOINCREMENT,
	 "ipAddress" TEXT(16,0) NOT NULL,
	 "username" TEXT(100,0) NOT NULL,
	 "password" TEXT NOT NULL,
	 "email" TEXT(100,0) NOT NULL,
	 "activationCode" TEXT(40,0) DEFAULT NULL,
	 "forgottenPasswordCode" TEXT(40,0) DEFAULT NULL,
	 "forgottenPasswordTime" INTEGER(11,0) NOT NULL DEFAULT 0,
	 "rememberCode" TEXT(40,0) DEFAULT NULL,
	 "createdOn" INTEGER(11,0) NOT NULL,
	 "lastLogin" INTEGER(11,0) DEFAULT NULL,
	 "active" INTEGER(1,0) DEFAULT NULL,
	 "firstName" TEXT(50,0) DEFAULT NULL,
	 "lastName" TEXT(50,0) DEFAULT NULL,
	 "company" TEXT(100,0) DEFAULT NULL,
	 "phone" TEXT(20,0) DEFAULT NULL
);



# Table structure for groups
# ------------------------------------------------------------

CREATE TABLE "groups" (
	 "id" INTEGER NOT NULL DEFAULT 0 PRIMARY KEY AUTOINCREMENT,
	 "name" TEXT(20,0) NOT NULL,
	 "description" TEXT(100,0) NOT NULL
);



# Table structure for users_groups
# ------------------------------------------------------------

CREATE TABLE "users_groups" (
	 "id" INTEGER NOT NULL PRIMARY KEY AUTOINCREMENT,
	 "userId" INTEGER(8,0) NOT NULL,
	 "groupId" INTEGER(8,0) NOT NULL,
	CONSTRAINT "Foreign_UsersID" FOREIGN KEY ("userId") REFERENCES "users" ("id") ON DELETE CASCADE ON UPDATE CASCADE,
	CONSTRAINT "Foreign_GroupsID" FOREIGN KEY ("groupId") REFERENCES "groups" ("id") ON DELETE RESTRICT ON UPDATE CASCADE
);



# Records of groups
# ------------------------------------------------------------

BEGIN;
INSERT INTO "groups" VALUES (1, 'admin', 'Administrator');
INSERT INTO "groups" VALUES (2, 'members', 'General User');
INSERT INTO "groups" VALUES (3, 'other', 'Other User');
COMMIT;



# Records of users
# ------------------------------------------------------------

BEGIN;
INSERT INTO "users" VALUES 
(1, '127.0.0.1', 'admin', 'U2FsdGVkX1/HRLAXNh9nUG3k6qJU5OeE9rACvDBL5WLsD7vPB9S44w==', 
'admin@admin.com', '', null, 0, null, 1361712014, 1361712014, 1, 
'Admin', 'Istrator', 'ADMIN', 0);
COMMIT;



# Records of users_groups
# ------------------------------------------------------------

BEGIN;
INSERT INTO "users_groups" VALUES (1, 1, 1);
INSERT INTO "users_groups" VALUES (2, 1, 2);
COMMIT;



# Table structure for login_attempts
# ------------------------------------------------------------

CREATE TABLE "login_attempts" (
	 "id" INTEGER NOT NULL DEFAULT 0 PRIMARY KEY AUTOINCREMENT,
	 "ipAddress" TEXT(16,0) NOT NULL,
	 "login" TEXT(100,0) NOT NULL,
	 "time" INTEGER(11,0) DEFAULT NULL
);
</code></pre>


<p class="important"><strong>Note:</strong> As of server version 7.0.6-rc-1 connecting to a SQLite database on Linux returns an error because revOpenDatabase() using SQLite is still not working in the Linux server engine. On Mac OS X connecting to SQLite should work as expected.</p>


<p class="important"><strong>Note:</strong> If, for security reasons, you need to validate session IDs please follow the instructions in the User Guide regarding <a href="sessions.html#savingSessionData">Saving Session Data to a Database</a> and create an additional table to store sessions.</p>


<a href="#top">Top of Page</a>

<a name="emailtemplates"></a>

<h2>Authentication Email Templates</h2>

<p>There are two email templates in system/application/views/emailTemplates used to send the user the "forgotten password" code, or to send an activation code to new users:</p>

<ul>
	<li>forgotPassword.lc</li>
	<li>authActivate.lc</li>
</ul>

<p>Customize these templates to best suit your needs. All you have to keep in mind is that the library provides the following variables to be used in your templates:</p>


<ul>
	<li><strong>gData["identity"]</strong><br />
		This is the user's email address or whatever is used to identify a user.</li>
	<li><strong>gData["forgottenPasswordCode"]</strong><br />
		This is the code to be sent to users who want to reset their password.</li>
	<li><strong>gData["activationCode"]</strong><br />
		This is the activation code to be sent to new users.</li>
</ul>


<p class="important"><strong>Note:</strong> Of course you can add your own variables to your custom templates as long as they are included in gData.</p>



<a href="#top">Top of Page</a>


<p>&nbsp;</p>

<a name="usage"></a>
<h1>Usage Example</h1>

<p>Before explaining what is needed to set up the authentication sample, let's describe the scenario:</p>

<ol>
	<li>There is an admin page which is used to manage user data (example.com/auth/).</li>
	<li>Opening this page without being logged in as a member of the admin group redirects you to a login page (example.com/auth/login/).</li>
	<li>A form is displayed on this page requiring you to fill in your credentials.</li>
	<li>Now if you submit something invalid, the form is redisplayed containing an error message describing the problem.</li>
	<li>If the verification of the data you supply is successful you will be redirected either to a member's page or to the admin page mentioned earlier, depending on which group you belong to.</li>
	<li>Then, let's say you forgot your password.</li>
	<li>Open the "Forgot Password" page, enter your email address and hit the send button (example.com/auth/forgotPassword/).</li>
	<li>Now, while you are redirected to the login page there is an email waiting for you containing a link to a "Reset Password" page (example.com/auth/resetPassword/).</li>
	<li>On this page enter a new password, confirm it and hit the submit button.</li>
	<li>You are redirected to the login page where you can enter your new password.</li>
	<li>To change your password open the "Change Password" page (example.com/auth/changePassword/), enter your email address and your password. Hit the submit button.</li>
	<li>The procedure which follows is similar to the forgotten password one.</li>
	<li>Now, let's say you logged in as a member of the admin group.</li>
	<li>You will be presented with the administration page (example.com/auth/).</li>
	<li>There you can create new users, activate / deactivate, edit and delete users.</li>
</ol>

<p>&nbsp;</p>

<p>The sample environment consists of the following files:</p>

<ol>
	<li>A <a href="../general/controllers.html">controller</a> to receive and process the submitted data.</li>
	<li>A form validation file.</li>
	<li>Various <a href="../general/views.html">view</a> files containing forms or a users table.</li>
	<li>A CSS file.</li>
</ol>

<p>Let's create those files.</p>

<a name="thecontroller"></a>

<h2>The Controller</h2>

<p>We start with the basic authentication controller script containing just the handler to load helpers respectively libraries and the default (index) handler. Create a controller called auth.lc. In it, place the code below and save it to your system/application/controllers/ folder.</p>

<pre><code>&lt;?rev

put "auth,index,login,logout,createUser,activate,deactivate,forgotPassword" & \
",resetPassword,changePassword,editUser,deleteUser" into gControllerHandlers


command auth
 # LOAD HELPERS
 put "form,url,asset" into tHelpers
 rigLoadHelper tHelpers

 # LOAD LIBRARIES
 rigLoaderLoadLibrary "Formvalidation"
 rigLoaderLoadLibrary "Table"
 rigLoaderLoadLibrary "Authentication"
end auth



command index
 # SET PAGE TITLE
 put "Auth Test Admin Page" into gData["pageTitle"]

 # CHECK IF USER IS LOGGED IN
 if rigAuthLoggedIn() is FALSE then
  # REDIRECT TO LOGIN PAGE AND KEEP FLASHDATA IN CASE THERE IS ANY
  if rigSessFlashdata("message") &lt;> FALSE then
   rigKeepSessFlashdata "message"
  end if

  rigRedirect "auth/login"

 else if rigAuthIsAdmin() is FALSE then
  # REDIRECT TO HOME PAGE BECAUSE USER IS NOT ALLOWED TO VIEW ADMIN CONTENT
  rigRedirect "/"

 else
  # ADMIN CONTENT
  #
  # SHOW VALIDATION ERRORS OR FLASH DATA IN CASE THERE IS ANY
  put rigValidationErrors("&lt;div class=" & quote & "error" & quote & ">", "&lt;/div>") into tErrors
  if tErrors &lt;> empty then
   put tErrors into gData["message"]
  else
   put rigSessFlashdata("message") into gData["message"]
   if gData["message"] is FALSE then
    put empty into gData["message"]
   end if
  end if

  # BUILD USERS LIST
  put rigAuthUsers() into tResult -- gData["users"]

  if tResult &lt;> FALSE then

   # BUILD MODIFIED RESULT ARRAY
   put 0 into tIncr
   repeat for each key tKey in tResult
    add 1 to tIncr

    put tResult[tIncr] into tRow
    # GET FIRST, LAST NAME AND EMAIL
    put tRow[13] into tResultData[tIncr][1]
    put tRow[14] into tResultData[tIncr][2]
    put tRow[5] into tResultData[tIncr][3]

    # GET USERS GROUPS
    put rigAuthUserGroups(tRow[1]) into tGroupsResult

    if tGroupsResult &lt;> FALSE then
     repeat for each key tKey in tGroupsResult
      put tGroupsResult[tKey] into tGroupsRow
      # GET NAME OF GROUP
      put tGroupsRow[2] & ", " after tResultData[tIncr][4]
     end repeat
     delete char -2 to -1 of tResultData[tIncr][4]
    end if

    # GET STATUS
    if tRow[12] is 1 then
     put rigAnchor("auth/deactivate/" & tRow[1], "Active") into tResultData[tIncr][5]
    else
     put rigAnchor("auth/activate/" & tRow[1], "Inactive") into tResultData[tIncr][5]
    end if

    # LINK TO EDIT USER
    put rigAnchor("auth/editUser/" & tRow[1], "Edit") into tResultData[tIncr][6]

    # LINK TO DELETE USER
    put rigAnchor("auth/deleteUser/" & tRow[1], "Delete") into tResultData[tIncr][7]
   end repeat

   # NOW GENERATE A USERS TABLE
   put "First Name", "Last Name", "Email", "Groups", "Status", "Edit, Delete" into tFieldnames
   rigSetTableHeading tFieldnames
   put rigGenerateTable(tResultData) into gData["usersTable"]
  end if

  # LINK TO LOGOUT
  put rigAnchor("auth/logout/", "Logout") into gData["logoutLink"]

  # LINK TO CREATE NEW USER
  put rigAnchor("auth/createUser/", "Create New User.") into gData["newUserLink"]

  # LOAD THE VIEW FILE
  get rigLoadView("auth/authMainView")
 end if
end index
</code></pre>

<a href="#top">Top of Page</a>

<p>Add the following code which is needed to create new user data.</p>

<pre><code>command createUser
 put "Auth Test Create User" into gData["pageTitle"]

 # CHECK IF USER IS NOT LOGGED IN OR IF USER IS NOT ADMIN
 if (rigAuthLoggedIn() is not TRUE) or (rigAuthIsAdmin() is not TRUE) then
  # REDIRECT TO ADMIN (LOGIN) PAGE
  rigRedirect "auth/"
 end if

 # FORM OPEN
 put "authForm" into tFormAttr["id"]
 put rigFormOpen("auth/createUser", tFormAttr) into gData["formOpen"]

 # FIELD SET
 put rigFormFieldset("User Data") into gData["fsOpen"]
 put rigFormFieldsetClose() into gData["fsClose"]

 # FORM LABELS
 put "formLabel" into tLabelAttr["class"]
 put rigFormLabel("First Name:", "first", tLabelAttr) into gData["firstLabel"]
 put rigFormLabel("Last Name:", "last", tLabelAttr) into gData["lastLabel"]
 put rigFormLabel("Company:", "company", tLabelAttr) into gData["companyLabel"]
 put rigFormLabel("Email:", "email", tLabelAttr) into gData["emailLabel"]
 put rigFormLabel("Phone:", "phone", tLabelAttr) into gData["phoneLabel"]
 put rigFormLabel("Password:", "password", tLabelAttr) into gData["passwordLabel"]
 put rigFormLabel("Confirm Password:", "passwordConfirm", tLabelAttr) into gData["passwordConfirmLabel"]

 # SUBMIT
 put "createSubmit" into tSubmitArr["name"]
 put "Create User" into tSubmitArr["value"]
 put "createBtn" into tSubmitArr["id"]
 put rigSubmitButton(tSubmitArr) into gData["createSubmit"]

 # FORM VALIDATION
 if rigFormValidRun("auth/createUser") is TRUE then
  # GET ACCOUNT DATA
  if $_POST is an array then
   put toLower(rigVarPOST("first")) && toLower(rigVarPOST("last")) into tUserName
   put rigVarPOST("password") into tPassword
   put rigVarPOST("email") into tEmail

   put rigVarPOST("first") into tExtraData["firstName"]
   put rigVarPOST("last") into tExtraData["lastName"]
   put rigVarPOST("company") into tExtraData["company"]
   put rigVarPOST("phone") into tExtraData["phone"]
  end if

  put rigAuthRegister(tUserName, tPassword, tEmail, tExtraData) into tRegistration

  # CHECK REGISTRATION DATA
  if tRegistration is an array then
   rigLogMessage "debug", "identity:" && tRegistration["identity"]
   rigLogMessage "debug", "id:" && tRegistration["id"]
   rigLogMessage "debug", "email:" && tRegistration["email"]
   rigLogMessage "debug", "activationCode:" && tRegistration["activationCode"]
  else
   rigLogMessage "debug", "tRegistration:" && tRegistration
  end if
  rigLogMessage "debug", "messages:" && rigAuthMessages()
  #

  # IF REGISTRATION WAS SUCCESSFUL REDIRECT TO AUTHENTICATION PAGE
  if tRegistration &lt;> FALSE then		
   put rigAuthMessages() into tMessages
   rigSetSessFlashdata "message", tMessages
   rigRedirect "/auth"
  else
   # LOAD VIEW
   put rigAuthErrors() into tErrors
   rigSetSessFlashdata "message", tErrors
   rigRedirect "auth/createUser"
  end if

 else -- if rigFormValidRun("auth/createUser") is TRUE then

  put rigValidationErrors("&lt;div class=" & quote & "error" & quote & ">", "&lt;/div>") into tErrors
  if tErrors &lt;> empty then
   put tErrors into gData["message"]
  else
   put rigSessFlashdata("message") into gData["message"]
   if gData["message"] is FALSE then
    put empty into gData["message"]
   end if
  end if

  # INPUT FIELDS
  put "first" into tInFirstArr["name"]
  # RE-POPULATE THE FIELD
  put rigSetValue("first") into tInFirstArr["value"]
  put "50" into tInFirstArr["maxlength"]
  put rigFormInput(tInFirstArr) into gData["first"]

  put "last" into tInLastArr["name"]
  # RE-POPULATE THE FIELD
  put rigSetValue("last") into tInLastArr["value"]
  put "50" into tInLastArr["maxlength"]
  put rigFormInput(tInLastArr) into gData["last"]

  put "company" into tInCompArr["name"]
  # RE-POPULATE THE FIELD
  put rigSetValue("company") into tInCompArr["value"]
  put "100" into tInCompArr["maxlength"]
  put rigFormInput(tInCompArr) into gData["company"]

  put "email" into tInMailArr["name"]
  # RE-POPULATE THE FIELD
  put rigSetValue("email") into tInMailArr["value"]
  put "50" into tInMailArr["maxlength"]
  put rigFormInput(tInMailArr) into gData["email"]

  put "phone" into tInPhoneArr["name"]
  # RE-POPULATE THE FIELD
  put rigSetValue("phone") into tInPhoneArr["value"]
  put "50" into tInPhoneArr["maxlength"]
  put rigFormInput(tInPhoneArr) into gData["phone"]

  put "password" into tInPassArr["name"]
  # RE-POPULATE THE FIELD
  put rigSetValue("password") into tInPassArr["value"]
  put "50" into tInPassArr["maxlength"]
  put rigFormInput(tInPassArr) into gData["password"]

  put "passwordConfirm" into tInPassConfArr["name"]
  # RE-POPULATE THE FIELD
  put rigSetValue("passwordConfirm") into tInPassConfArr["value"]
  put "50" into tInPassConfArr["maxlength"]
  put rigFormInput(tInPassConfArr) into gData["passwordConfirm"]
 end if

 # LOAD VIEW
 get rigLoadView("auth/createUserMainView")
end createUser
</code></pre>

<a href="#top">Top of Page</a>

<p>Add the login handler.</p>

<pre><code>command login
 put "Auth Test Login" into gData["pageTitle"]

 # FORM OPEN
 put "authForm" into tFormAttr["id"]
 put rigFormOpen("auth/login", tFormAttr) into gData["formOpen"]

 # FIELD SET
 put rigFormFieldset("Login Form") into gData["fsOpen"]
 put rigFormFieldsetClose() into gData["fsClose"]

 # FORM LABELS
 put "formLabel" into tLabelAttr["class"]
 put rigFormLabel("Email/Username:", "identity", tLabelAttr) into gData["identityLabel"]
 put rigFormLabel("Password:", "password", tLabelAttr) into gData["passwordLabel"]
 put rigFormLabel("Remember me:", "remember", tLabelAttr) into gData["rememberLabel"]

 # SUBMIT
 put "loginSubmit" into tSubmitArr["name"]
 put "Login" into tSubmitArr["value"]
 put "loginBtn" into tSubmitArr["id"]
 put rigSubmitButton(tSubmitArr) into gData["loginSubmit"]

 # FORM VALIDATION
 if rigFormValidRun("auth/login") is TRUE then

  # CHECK IF USER IS LOGGING IN		
  # REMEMBER ME CHECK BOX
  put FALSE into tRemember
  # CHECK IF THERE IS A POST VARIABLE remember
  put rigVarPost("remember") into tPostRemember
  if tPostRemember &lt;> FALSE then
   # CHECK VALUE
   if tPostRemember[1] is 1 then
    put TRUE into tRemember
   end if
  end if

  get rigAuthLogin(rigVarPost("identity"), rigVarPost("password"), tRemember)

  # IF LOGIN IS SUCCESSFUL REDIRECT BACK TO HOME PAGE, auth IN THIS CASE
  if it is TRUE then
   put rigAuthMessages() into tMessages
   rigSetSessFlashdata "message", tMessages
   rigRedirect "/auth"

  else
   # IF LOGIN IS UNSUCCESSFUL REDIRECT BACK TO THE LOGIN PAGE
   put rigAuthErrors() into tErrors
   rigSetSessFlashdata "message", tErrors
   rigRedirect "auth/login"
  end if


 else
  # THE USER IS NOT LOGGING IN, SHOW THE LOGIN PAGE
  # SHOW VALIDATION ERRORS OR FLASH DATA IF THERE IS ANY
  put rigValidationErrors("&lt;div class=" & quote & "error" & quote & ">", "&lt;/div>") into tErrors
  if tErrors &lt;> empty then
   put tErrors into gData["message"]
  else
   put rigSessFlashdata("message") into gData["message"]
   if gData["message"] is FALSE then
    put empty into gData["message"]
   end if
  end if

  # INPUT FIELDS
  put "identity" into tInIdentArr["name"]
  # RE-POPULATE THE FIELD
  put rigSetValue("identity") into tInIdentArr["value"]
  put "100" into tInIdentArr["maxlength"]
  put rigFormInput(tInIdentArr) into gData["identity"]

  put "password" into tInPwArr["name"]
  put "40" into tInPwArr["maxlength"]
  put rigFormInput(tInPwArr) into gData["loginPassword"]

  # CHECKBOX
  put rigSetCheckbox("remember[]", "1", TRUE) into gData["chbRemember"]

  get rigLoadView("auth/loginMainView")

 end if -- if rigFormValidRun("auth/login") is TRUE then
end login
</code></pre>

<a href="#top">Top of Page</a>

<p>Of course there is a logout handler.</p>

<pre><code>command logout
 # LOG USER OUT
 rigAuthLogout

 # REDIRECT TO THE LOGIN PAGE
 put rigAuthMessages() into tMessages

 rigSetSessFlashdata "message", tMessages
 rigRedirect "/auth/login"
end logout
</code></pre>

<a href="#top">Top of Page</a>

<p>Add the following user activation handler.</p>

<pre><code>command activate
 # GET USER ID AND ACTIVATION CODE
 put rigFetchSegment(3) into tID
 put rigFetchSegment(4) into tCode
 put FALSE into tActivation

 # ACTIVATE USER
 if tCode &lt;> FALSE then
  put rigAuthActivate(tID, tCode) into tActivation

 else if rigAuthIsAdmin() is TRUE then
  put rigAuthActivate(tID) into tActivation
 end if

 # CHECK IF ACTIVATION WAS SUCCESSFUL AND ACT ACCORDINGLY
 if tActivation is TRUE then
  # REDIRECT TO THE AUTH PAGE
  put rigAuthMessages() into tMessages

  rigSetSessFlashdata "message", tMessages
  rigRedirect "/auth"

 else
  put rigAuthErrors() into tErrors

  rigSetSessFlashdata "message", tErrors
  rigRedirect "auth/forgotPassword"
 end if	
end activate
</code></pre>

<a href="#top">Top of Page</a>

<p>Add the user deactivation handler.</p>

<pre><code>command deactivate
 put "Auth Test Deactivate" into gData["pageTitle"]

 put rigFetchSegment(3) into tID

 # FORM OPEN
 put "authForm" into tFormAttr["id"]
 put rigFormOpen("auth/deactivate/" & tID, tFormAttr) into gData["formOpen"]

 # FIELD SET
 put rigFormFieldset("Deactivate User Form") into gData["fsOpen"]
 put rigFormFieldsetClose() into gData["fsClose"]

 # FORM LABELS
 put "formLabel" into tLabelAttr["class"]
 put rigFormLabel("Yes:", "confirm", tLabelAttr) into gData["confirmRadioYesLabel"]
 put rigFormLabel("No:", "confirm", tLabelAttr) into gData["confirmRadioNoLabel"]

 # SUBMIT
 put "deactivateSubmit" into tSubmitArr["name"]
 put "Submit" into tSubmitArr["value"]
 put "submitBtn" into tSubmitArr["id"]
 put rigSubmitButton(tSubmitArr) into gData["deactivateSubmit"]

 # FORM VALIDATION
 if rigFormValidRun("auth/deactivate") is TRUE then
  put FALSE into tDeactivated

  # CHECK IF DEACTIVATION WAS CONFIRMED
  if rigVarPOST("confirm") is "yes" then

   # VALIDATE THE REQUEST
   if tID &lt;> rigVarPOST("id") then
    rigLogMessage "error", "This user deactivation form post did not pass the security check.", TRUE
   end if

   # CHECK THE USER LEVEL
   if (rigAuthLoggedIn() is TRUE) and (rigAuthIsAdmin() is TRUE) then
    put rigAuthDeactivate(tID) into tDeactivated
   end if

  end if

  # GET AUTH MESSAGES AND REDIRECT TO THE AUTH PAGE
  if tDeactivated is TRUE then
   put rigAuthMessages() into tMessages
  else
   put rigAuthErrors() into tMessages
  end if

  rigSetSessFlashdata "message", tMessages
  rigRedirect "/auth"

 else
  put rigValidationErrors("&lt;div class=" & quote & "error" & quote & ">", "&lt;/div>") into tErrors
  if tErrors &lt;> empty then
   put tErrors into gData["message"]
  else
   put rigSessFlashdata("message") into gData["message"]
   if gData["message"] is FALSE then
    put empty into gData["message"]
   end if
  end if

  # HIDDEN USER ID FIELD
  put rigAuthUser(tID) into tQueryResult
  if (tQueryResult &lt;> FALSE) and (tQueryResult is an array) then
   put rigDbRow() into tUserRow
   put tUserRow["id"] into tUserID
   put tUserRow["username"] into gData["userName"]
  else
   put empty into tUserID
  end if
  put rigFormHidden("id", tUserID) into gData["hiddenUserID"]

  # RADIO BUTTONS
  put rigSetRadio("confirm", "yes") into gData["confirmYes"]
  put rigSetRadio("confirm", "no", TRUE) into gData["confirmNo"]

  # LOAD VIEWS
  get rigLoadView("auth/deactivateUserMainView")
 end if
end deactivate
</code></pre>

<a href="#top">Top of Page</a>

<p>Add the "forgotPassword" handler.</p>

<pre><code>command forgotPassword
 put "Auth Test Forgot Password" into gData["pageTitle"]

 put rigSessFlashdata("message") into gData["message"]

 # FORM OPEN
 put "authForm" into tFormAttr["id"]
 put rigFormOpen("auth/forgotPassword/", tFormAttr) into gData["formOpen"]

 # FIELD SET
 put rigFormFieldset("Forgot Password Form") into gData["fsOpen"]
 put rigFormFieldsetClose() into gData["fsClose"]

 # FORM LABELS
 put "formLabel" into tLabelAttr["class"]
 put rigFormLabel("Email:", "email", tLabelAttr) into gData["emailLabel"]

 # SUBMIT
 put "forgotSubmit" into tSubmitArr["name"]
 put "Submit" into tSubmitArr["value"]
 put "forgotBtn" into tSubmitArr["id"]
 put rigSubmitButton(tSubmitArr) into gData["forgotSubmit"]

 # FORM VALIDATION
 if rigFormValidRun("auth/forgotPassword") is TRUE then
  # CALL THE rigAuthForgottenPassword FUNCTION TO SEND A RESET CODE TO THE USER
  put rigAuthForgottenPassword(rigVarPOST("email")) into tResetPasswordSent

  if tResetPasswordSent is TRUE then
   put rigAuthMessages() into tMessages

   rigSetSessFlashdata "message", tMessages
   rigRedirect "/auth/login"
  else
   put rigAuthErrors() into tErrors

   rigSetSessFlashdata "message", tErrors
   rigRedirect "auth/forgotPassword"
  end if		

 else
  put rigValidationErrors("&lt;div class=" & quote & "error" & quote & ">", "&lt;/div>") into tErrors
  if tErrors &lt;> empty then
   put tErrors into gData["message"]
  else
   put rigSessFlashdata("message") into gData["message"]
   if gData["message"] is FALSE then
    put empty into gData["message"]
   end if
  end if

  # INPUT FIELDS
  put "email" into tInMailArr["name"]
  # RE-POPULATE THE FIELD
  put rigSetValue("email") into tInMailArr["value"]
  put "50" into tInMailArr["maxlength"]
  put rigFormInput(tInMailArr) into gData["email"]
 end if

 # LOAD VIEW
 get rigLoadView("auth/forgotPasswordMainView")
end forgotPassword
</code></pre>

<a href="#top">Top of Page</a>

<p>Add code which is used to reset passwords in case a user forgot the password or sent a change password request.</p>

<pre><code>command resetPassword
 put "Auth Test Reset Password" into gData["pageTitle"]

 put rigFetchSegment(3) into tForgottenPasswordCode

 if tForgottenPasswordCode is FALSE then
  rigShow404
 end if

 put rigAuthForgottenPasswordCheck(tForgottenPasswordCode) into tUser

 if tUser &lt;> FALSE then
  put rigAuthFetchConfigItem("minLengthPassword") into tMinPasswordLength
  put rigAuthFetchConfigItem("maxLengthPassword") into tMaxPasswordLength

  # FORM OPEN
  put "authForm" into tFormAttr["id"]
  put rigFormOpen("auth/resetPassword/" & tForgottenPasswordCode, tFormAttr) into gData["formOpen"]

  # FIELD SET
  put rigFormFieldset("Reset Password Form") into gData["fsOpen"]
  put rigFormFieldsetClose() into gData["fsClose"]

  # FORM LABELS
  put "formLabel" into tLabelAttr["class"]
  put rigFormLabel("New Password (at least" && tMinPasswordLength && "characters long):", \
"newPassword", tLabelAttr) into gData["newPasswordLabel"]
  put rigFormLabel("Confirm New Password:", "newPasswordConfirm", tLabelAttr) into gData["newPasswordConfirmLabel"]

  # SUBMIT
  put "resetSubmit" into tSubmitArr["name"]
  put "Change" into tSubmitArr["value"]
  put "resetBtn" into tSubmitArr["id"]
  put rigSubmitButton(tSubmitArr) into gData["resetSubmit"]

  # IF THE CODE IS VALID DISPLAY THE PASSWORD RESET FORM
  # FORM VALIDATION
  if rigFormValidRun("auth/resetPassword") is TRUE then

   # VALIDATE THE REQUEST
   if tUser["id"] &lt;> rigVarPOST("hiddenUserID") then
    get rigAuthClearForgottenPasswordCode(tForgottenPasswordCode)
    rigLogMessage "error", "This reset password form post did not pass the security check.", TRUE

   else
    # CHANGE THE PASSWORD
    put rigAuthFetchConfigItem("identityColumn") into tIdentityCol
    put tUser[tIdentityCol] into tIdentity

    put rigAuthResetPassword(tIdentity, rigVarPOST("newPassword")) into tChange

    if tChange is TRUE then
     # THE PASSWORD WAS SUCCESSFULLY CHANGED
     put rigAuthMessages() into tMessages

     rigSetSessFlashdata "message", tMessages
     logout

    else
     put rigAuthErrors() into tErrors

     rigSetSessFlashdata "message", tErrors
     rigRedirect "auth/resetPassword/" & tForgottenPasswordCode
    end if

   end if

  else
   # DISPLAY THE FORM
   # SET THE FLASH DATA ERROR MESSAGE IF THERE IS ONE
   put rigValidationErrors("&lt;div class=" & quote & "error" & quote & ">", "&lt;/div>") into tErrors
   if tErrors &lt;> empty then
    put tErrors into gData["message"]
   else
    put rigSessFlashdata("message") into gData["message"]
    if gData["message"] is FALSE then
     put empty into gData["message"]
    end if
   end if

   # INPUT FIELDS
   put "newPassword" into tInPassArr["name"]
   put tMaxPasswordLength into tInPassArr["maxlength"]
   put "password" into tInPassArr["type"]
   put rigFormInput(tInPassArr) into gData["newPassword"]

   put "newPasswordConfirm" into tInPassConfArr["name"]
   put tMaxPasswordLength into tInPassConfArr["maxlength"]
   put "password" into tInPassConfArr["type"]
   put rigFormInput(tInPassConfArr) into gData["newPasswordConfirm"]

   put "hiddenUserID" into tInUserIdArr["name"]
   put tUser["id"] into tInUserIdArr["value"]
   put "hidden" into tInUserIdArr["type"]
   put rigFormInput(tInUserIdArr) into gData["hiddenUserID"]

   # LOAD VIEW
   get rigLoadView("auth/resetPasswordMainView")

  end if -- if rigFormValidRun("auth/forgotPassword") is TRUE then

 else  -- if tUser &lt;> FALSE then
  put rigAuthErrors() into tErrors

  rigSetSessFlashdata "message", tErrors
  rigRedirect "auth/forgotPassword"
 end if -- if tUser &lt;> FALSE then

end resetPassword
</code></pre>

<a href="#top">Top of Page</a>


<p>Add a handler which is used to change passwords.</p>

<pre><code>command changePassword
 put "Auth Test Change Password" into gData["pageTitle"]

 # CHECK IF USER IS LOGGED IN
 if rigAuthLoggedIn() is FALSE then
  rigRedirect "/auth/login"
 end if

 # GET USER DATA
 put rigAuthUser() into tQueryResult

 if (tQueryResult &lt;> FALSE) and (tQueryResult is an array) then
  put rigDbRow() into tUser
 end if

 # GET MIN / MAX PASSWORD LENGTH
 put rigAuthFetchConfigItem("minLengthPassword") into tMinPasswordLength
 put rigAuthFetchConfigItem("maxLengthPassword") into tMaxPasswordLength

 # FORM OPEN
 put "authForm" into tFormAttr["id"]
 put rigFormOpen("auth/changePassword/", tFormAttr) into gData["formOpen"]

 # FIELD SET
 put rigFormFieldset("Change Password Form") into gData["fsOpen"]
 put rigFormFieldsetClose() into gData["fsClose"]

 # FORM LABELS
 put "formLabel" into tLabelAttr["class"]
 put rigFormLabel("Old Password:", "oldPassword", tLabelAttr) into gData["oldPasswordLabel"]
 put rigFormLabel("New Password (at least" && tMinPasswordLength && "characters long):", \
"newPassword", tLabelAttr) into gData["newPasswordLabel"]
 put rigFormLabel("Confirm New Password:", "newPasswordConfirm", tLabelAttr) into gData["newPasswordConfirmLabel"]

 # SUBMIT
 put "changeSubmit" into tSubmitArr["name"]
 put "Change" into tSubmitArr["value"]
 put "changeBtn" into tSubmitArr["id"]
 put rigSubmitButton(tSubmitArr) into gData["changeSubmit"]

 # FORM VALIDATION
 if rigFormValidRun("auth/changePassword") is TRUE then
  put rigAuthFetchConfigItem("identityColumn") into tIdentityCol
  put rigSessUserdata(tIdentityCol) into tIdentity

  put rigAuthChangePassword(tIdentity, rigVarPOST("oldPassword"), rigVarPOST("newPassword")) into tChange

  if tChange is TRUE then
   # THE PASSWORD WAS SUCCESSFULLY CHANGED
   put rigAuthMessages() into tMessages

   rigSetSessFlashdata "message", tMessages
   logout

  else
   put rigAuthErrors() into tErrors

   rigSetSessFlashdata "message", tErrors
   rigRedirect "auth/changePassword/" & tForgottenPasswordCode
  end if

 else
  # DISPLAY THE FORM
  # SET THE FLASH DATA ERROR MESSAGE IF THERE IS ONE
  put rigValidationErrors("&lt;div class=" & quote & "error" & quote & ">", "&lt;/div>") into tErrors
  if tErrors &lt;> empty then
   put tErrors into gData["message"]
  else
   put rigSessFlashdata("message") into gData["message"]
   if gData["message"] is FALSE then
    put empty into gData["message"]
   end if
  end if

  # INPUT FIELDS
  put "oldPassword" into tInOldPassArr["name"]
  put tMaxPasswordLength into tInOldPassArr["maxlength"]
  put "password" into tInOldPassArr["type"]
  put rigFormInput(tInOldPassArr) into gData["oldPassword"]

  put "newPassword" into tInPassArr["name"]
  put tMaxPasswordLength into tInPassArr["maxlength"]
  put "password" into tInPassArr["type"]
  put rigFormInput(tInPassArr) into gData["newPassword"]

  put "newPasswordConfirm" into tInPassConfArr["name"]
  put tMaxPasswordLength into tInPassConfArr["maxlength"]
  put "password" into tInPassConfArr["type"]
  put rigFormInput(tInPassConfArr) into gData["newPasswordConfirm"]

  put "hiddenUserID" into tInUserIdArr["name"]
  put tUser["id"] into tInUserIdArr["value"]
  put "hidden" into tInUserIdArr["type"]
  put rigFormInput(tInUserIdArr) into gData["hiddenUserID"]

  # LOAD VIEW
  get rigLoadView("auth/changePasswordMainView")

 end if -- if rigFormValidRun("auth/resetPassword") is TRUE then
end changePassword
</code></pre>

<a href="#top">Top of Page</a>

<p>Add a handler which is used to edit user data.</p>

<pre><code>command editUser
 put "Auth Test Edit User" into gData["pageTitle"]

 # CHECK IF USER IS NOT LOGGED IN OR IF USER IS NOT ADMIN
 if (rigAuthLoggedIn() is not TRUE) or (rigAuthIsAdmin() is not TRUE) then
  # REDIRECT TO AUTH PAGE
  rigRedirect "auth/"
 end if

 # GET MIN / MAX PASSWORD LENGTH
 put rigAuthFetchConfigItem("minLengthPassword") into tMinPasswordLength
 put rigAuthFetchConfigItem("maxLengthPassword") into tMaxPasswordLength

 # GET USER ID
 put rigFetchSegment(3) into tID

 # GET STORED USER DATA
 put rigAuthUser(tID) into tQueryResult
 if (tQueryResult &lt;> FALSE) and (tQueryResult is an array) then
  put rigDbRow() into tUser
 end if

 # FORM OPEN
 put "authForm" into tFormAttr["id"]
 put rigFormOpen("auth/editUser/" & tID, tFormAttr) into gData["formOpen"]

 # FIELD SET
 put rigFormFieldset("Edit User") into gData["fsOpen"]
 put rigFormFieldsetClose() into gData["fsClose"]

 # FORM LABELS
 put "formLabel" into tLabelAttr["class"]
 put rigFormLabel("First Name:", "first", tLabelAttr) into gData["firstLabel"]
 put rigFormLabel("Last Name:", "last", tLabelAttr) into gData["lastLabel"]
 put rigFormLabel("Company:", "company", tLabelAttr) into gData["companyLabel"]
 put rigFormLabel("Phone:", "phone", tLabelAttr) into gData["phoneLabel"]
 put rigFormLabel("Password:", "password", tLabelAttr) into gData["passwordLabel"]
 put rigFormLabel("Confirm Password:", "passwordConfirm", tLabelAttr) into gData["passwordConfirmLabel"]

 # SUBMIT
 put "editSubmit" into tSubmitArr["name"]
 put "Save User" into tSubmitArr["value"]
 put "editBtn" into tSubmitArr["id"]
 put rigSubmitButton(tSubmitArr) into gData["editSubmit"]

 # CHECK POST ARRAY DATA
 if $_POST is an array then
  # VALIDATE THE REQUEST
  if tID &lt;> rigVarPOST("hiddenUserID") then
   rigLogMessage "error", "This form post did not pass the security check.", TRUE
  end if

  put rigVarPost("first") into tData["firstName"]
  put rigVarPost("last") into tData["lastName"]
  put rigVarPost("company") into tData["company"]
  put rigVarPost("phone") into tData["phone"]

  # UPDATE PASSWORD IF IT WAS POSTED
  if rigVarPost("password") &lt;> empty then
   rigSetRules "password", "Password", \
"requiredR|minLengthR[" & tMinPasswordLength & "]|maxLengthR[" & tMaxPasswordLength & "]|matchesR[passwordConfirm]|xssClean"
   rigSetRules "passwordConfirm", "Confirm Password", "requiredR"

   put rigVarPost("password") into tData["password"]
  end if

  # FORM VALIDATION
  if rigFormValidRun("auth/editUser") is TRUE then
   get rigAuthUpdate(tUser["id"], tData)

   rigSetSessFlashdata "message", "User saved."
   # REDIRECT TO AUTH PAGE
   rigRedirect "auth/"
  end if

 end if -- if $_POST is an array then
 put rigValidationErrors("&lt;div class=" & quote & "error" & quote & ">", "&lt;/div>") into tErrors
 if tErrors &lt;> empty then
  put tErrors into gData["message"]
 else
  put rigSessFlashdata("message") into gData["message"]
  if gData["message"] is FALSE then
   put empty into gData["message"]
  end if
 end if

 # INPUT FIELDS
 put "first" into tInFirstArr["name"]
 # RE-POPULATE THE FIELD
 put rigSetValue("first", tUser["firstName"]) into tInFirstArr["value"]
 put "50" into tInFirstArr["maxlength"]
 put rigFormInput(tInFirstArr) into gData["first"]

 put "last" into tInLastArr["name"]
 # RE-POPULATE THE FIELD
 put rigSetValue("last", tUser["lastName"]) into tInLastArr["value"]
 put "50" into tInLastArr["maxlength"]
 put rigFormInput(tInLastArr) into gData["last"]

 put "company" into tInCompArr["name"]
 # RE-POPULATE THE FIELD
 put rigSetValue("company", tUser["company"]) into tInCompArr["value"]
 put "100" into tInCompArr["maxlength"]
 put rigFormInput(tInCompArr) into gData["company"]

 put "phone" into tInPhoneArr["name"]
 # RE-POPULATE THE FIELD
 put rigSetValue("phone", tUser["phone"]) into tInPhoneArr["value"]
 put "50" into tInPhoneArr["maxlength"]
 put rigFormInput(tInPhoneArr) into gData["phone"]

 put "password" into tInPassArr["name"]
 # RE-POPULATE THE FIELD
 put rigSetValue("password") into tInPassArr["value"]
 put "50" into tInPassArr["maxlength"]
 put rigFormInput(tInPassArr) into gData["password"]

 put "passwordConfirm" into tInPassConfArr["name"]
 # RE-POPULATE THE FIELD
 put rigSetValue("passwordConfirm") into tInPassConfArr["value"]
 put "50" into tInPassConfArr["maxlength"]
 put rigFormInput(tInPassConfArr) into gData["passwordConfirm"]

 put "hiddenUserID" into tInUserIdArr["name"]
 put tID into tInUserIdArr["value"]
 put "hidden" into tInUserIdArr["type"]
 put rigFormInput(tInUserIdArr) into gData["hiddenUserID"]

 # LOAD VIEW
 get rigLoadView("auth/editUserMainView")

end editUser
</code></pre>

<a href="#top">Top of Page</a>

<p>Now add the last handler which is needed to delete user data.</p>

<pre><code>command deleteUser
 put "Auth Test Delete User" into gData["pageTitle"]

 put rigFetchSegment(3) into tID

 # FORM OPEN
 put "authForm" into tFormAttr["id"]
 put rigFormOpen("auth/deleteUser/" & tID, tFormAttr) into gData["formOpen"]

 # FIELD SET
 put rigFormFieldset("Delete User Form") into gData["fsOpen"]
 put rigFormFieldsetClose() into gData["fsClose"]

 # FORM LABELS
 put "formLabel" into tLabelAttr["class"]
 put rigFormLabel("Yes:", "confirm", tLabelAttr) into gData["confirmRadioYesLabel"]
 put rigFormLabel("No:", "confirm", tLabelAttr) into gData["confirmRadioNoLabel"]

 # SUBMIT
 put "deleteSubmit" into tSubmitArr["name"]
 put "Submit" into tSubmitArr["value"]
 put "submitBtn" into tSubmitArr["id"]
 put rigSubmitButton(tSubmitArr) into gData["deleteSubmit"]

 # FORM VALIDATION
 if rigFormValidRun("auth/deleteUser") is TRUE then
  put FALSE into tDeleted

  # CHECK IF DEACTIVATION WAS CONFIRMED
  if rigVarPOST("confirm") is "yes" then

   # VALIDATE THE REQUEST
   if tID &lt;> rigVarPOST("id") then
    rigLogMessage "error", "This form post did not pass the security check.", TRUE
   end if

   # CHECK THE USER LEVEL
   if (rigAuthLoggedIn() is TRUE) and (rigAuthIsAdmin() is TRUE) then
    put rigAuthDeleteUser(tID) into tDeleted
   end if

  end if

  # GET AUTH MESSAGES AND REDIRECT TO THE AUTH PAGE
  if tDeleted is TRUE then
   put rigAuthMessages() into tMessages
  else
   put rigAuthErrors() into tMessages
  end if

  rigSetSessFlashdata "message", tMessages
  rigRedirect "/auth"

 else
  put rigValidationErrors("&lt;div class=" & quote & "error" & quote & ">", "&lt;/div>") into tErrors
  if tErrors &lt;> empty then
   put tErrors into gData["message"]
  else
   put rigSessFlashdata("message") into gData["message"]
   if gData["message"] is FALSE then
    put empty into gData["message"]
   end if
  end if

  # HIDDEN USER ID FIELD
  put rigAuthUser(tID) into tQueryResult

  if (tQueryResult &lt;> FALSE) and (tQueryResult is an array) then
   put rigDbRow() into tUserRow
   put tUserRow["id"] into tUserID
   put tUserRow["username"] into gData["userName"]
  else
   put empty into tUserID
  end if
  put rigFormHidden("id", tUserID) into gData["hiddenUserID"]

  # RADIO BUTTONS
  put rigSetRadio("confirm", "yes") into gData["confirmYes"]
  put rigSetRadio("confirm", "no", TRUE) into gData["confirmNo"]

  # LOAD VIEW
  get rigLoadView("auth/deleteUserMainView")
 end if
end deleteUser






--| END OF auth.lc
--| Location: ./system/application/controllers/auth.lc
----------------------------------------------------------------------
</code></pre>



<a href="#top">Top of Page</a>




<a name="thevalidationfile"></a>

<h2>The Form Validation File</h2>

<p>The rules to validate authentication. Create a file called validation.lc. In it, place the code below and save it to your system/application/config/ folder.</p>


<pre><code>&lt;?rev
put gBASEPATH into gBASEPATH

if gBASEPATH is "gBASEPATH" then
 put "No direct script access allowed."
 exit to top
end if


local sValidationConf


# AUTHENTICATION VALIDATION
# LOGIN
put "identity" into sValidationConf["auth/login"][1]["field"]
put "Email" into sValidationConf["auth/login"][1]["label"]
put "trim|requiredR|validEmailR|maxLengthR[100]|xssClean" into sValidationConf["auth/login"][1]["rules"]

put "password" into sValidationConf["auth/login"][2]["field"]
put "Password" into sValidationConf["auth/login"][2]["label"]
put "requiredR|alphaDashR|maxLengthR[40]|xssClean" into sValidationConf["auth/login"][2]["rules"]

put "remember[]" into sValidationConf["auth/login"][3]["field"]
put "Remember me" into sValidationConf["auth/login"][3]["label"]
put "numericR" into sValidationConf["auth/login"][3]["rules"]


# CREATE USER
put "first" into sValidationConf["auth/createUser"][1]["field"]
put "First Name" into sValidationConf["auth/createUser"][1]["label"]
put "trim|requiredR|minLengthR[3]|maxLengthR[50]|alphaDashR|xssClean" into sValidationConf["auth/createUser"][1]["rules"]

put "last" into sValidationConf["auth/createUser"][2]["field"]
put "Last Name" into sValidationConf["auth/createUser"][2]["label"]
put "trim|requiredR|minLengthR[3]|maxLengthR[50]|alphaDashR|xssClean" into sValidationConf["auth/createUser"][2]["rules"]

put "company" into sValidationConf["auth/createUser"][3]["field"]
put "Company" into sValidationConf["auth/createUser"][3]["label"]
put "trim|minLengthR[3]|maxLengthR[100]|alphaDashR|xssClean" into sValidationConf["auth/createUser"][3]["rules"]

put "email" into sValidationConf["auth/createUser"][4]["field"]
put "Email" into sValidationConf["auth/createUser"][4]["label"]
put "trim|requiredR|validEmailR|minLengthR[3]|maxLengthR[100]|xssClean" into sValidationConf["auth/createUser"][4]["rules"]

put "phone" into sValidationConf["auth/createUser"][5]["field"]
put "Phone" into sValidationConf["auth/createUser"][5]["label"]
put "trim|alphaDashR|maxLengthR[20]" into sValidationConf["auth/createUser"][5]["rules"]

put "password" into sValidationConf["auth/createUser"][6]["field"]
put "Password" into sValidationConf["auth/createUser"][6]["label"]
put "requiredR|minLengthR[8]|maxLengthR[40]|matchesR[passwordConfirm]|xssClean" into sValidationConf["auth/createUser"][6]["rules"]

put "passwordConfirm" into sValidationConf["auth/createUser"][7]["field"]
put "Confirm Password" into sValidationConf["auth/createUser"][7]["label"]
put "requiredR" into sValidationConf["auth/createUser"][7]["rules"]


# DEACTIVATE USER
put "confirm" into sValidationConf["auth/deactivate"][1]["field"]
put "Confirmation" into sValidationConf["auth/deactivate"][1]["label"]
put "requiredR|alphaR" into sValidationConf["auth/deactivate"][1]["rules"]

put "id" into sValidationConf["auth/deactivate"][2]["field"]
put "User ID" into sValidationConf["auth/deactivate"][2]["label"]
put "requiredR|numericR" into sValidationConf["auth/deactivate"][2]["rules"]


# FORGOT PASSWORD
put "email" into sValidationConf["auth/forgotPassword"][1]["field"]
put "Email" into sValidationConf["auth/forgotPassword"][1]["label"]
put "trim|requiredR|validEmailR|minLengthR[3]|maxLengthR[100]|xssClean" into sValidationConf["auth/forgotPassword"][1]["rules"]


# RESET PASSWORD
put "newPassword" into sValidationConf["auth/resetPassword"][1]["field"]
put "New Password" into sValidationConf["auth/resetPassword"][1]["label"]
put "requiredR|minLengthR[8]|maxLengthR[20]|matchesR[newPasswordConfirm]|xssClean" into sValidationConf["auth/resetPassword"][1]["rules"]

put "newPasswordConfirm" into sValidationConf["auth/resetPassword"][2]["field"]
put "Confirm New Password" into sValidationConf["auth/resetPassword"][2]["label"]
put "requiredR" into sValidationConf["auth/resetPassword"][2]["rules"]


# CHANGE PASSWORD
put "oldPassword" into sValidationConf["auth/changePassword"][1]["field"]
put "Old Password" into sValidationConf["auth/changePassword"][1]["label"]
put "requiredR" into sValidationConf["auth/changePassword"][1]["rules"]

put "newPassword" into sValidationConf["auth/changePassword"][2]["field"]
put "New Password" into sValidationConf["auth/changePassword"][2]["label"]
put "requiredR|minLengthR[8]|maxLengthR[20]|matchesR[newPasswordConfirm]|xssClean" into sValidationConf["auth/changePassword"][2]["rules"]

put "newPasswordConfirm" into sValidationConf["auth/changePassword"][3]["field"]
put "Confirm New Password" into sValidationConf["auth/changePassword"][3]["label"]
put "requiredR" into sValidationConf["auth/changePassword"][3]["rules"]


# EDIT USER
put "first" into sValidationConf["auth/editUser"][1]["field"]
put "First Name" into sValidationConf["auth/editUser"][1]["label"]
put "trim|requiredR|minLengthR[3]|maxLengthR[50]|alphaDashR|xssClean" into sValidationConf["auth/editUser"][1]["rules"]

put "last" into sValidationConf["auth/editUser"][2]["field"]
put "Last Name" into sValidationConf["auth/editUser"][2]["label"]
put "trim|requiredR|minLengthR[3]|maxLengthR[50]|alphaDashR|xssClean" into sValidationConf["auth/editUser"][2]["rules"]

put "company" into sValidationConf["auth/editUser"][3]["field"]
put "Company" into sValidationConf["auth/editUser"][3]["label"]
put "trim|minLengthR[3]|maxLengthR[100]|alphaDashR|xssClean" into sValidationConf["auth/editUser"][3]["rules"]

put "phone" into sValidationConf["auth/editUser"][4]["field"]
put "Phone" into sValidationConf["auth/editUser"][4]["label"]
put "trim|alphaDashR|maxLengthR[20]" into sValidationConf["auth/editUser"][4]["rules"]

put "password" into sValidationConf["auth/editUser"][5]["field"]
put "Password" into sValidationConf["auth/editUser"][5]["label"]
put "minLengthR[8]|maxLengthR[40]|matchesR[passwordConfirm]|xssClean" into sValidationConf["auth/editUser"][5]["rules"]

put "passwordConfirm" into sValidationConf["auth/editUser"][6]["field"]
put "Confirm Password" into sValidationConf["auth/editUser"][6]["label"]
put "minLengthR[8]" into sValidationConf["auth/editUser"][6]["rules"]


# DELETE USER
put "confirm" into sValidationConf["auth/deleteUser"][1]["field"]
put "Confirmation" into sValidationConf["auth/deleteUser"][1]["label"]
put "requiredR|alphaR" into sValidationConf["auth/deleteUser"][1]["rules"]

put "id" into sValidationConf["auth/deleteUser"][2]["field"]
put "User ID" into sValidationConf["auth/deleteUser"][2]["label"]
put "requiredR|numericR" into sValidationConf["auth/deleteUser"][2]["rules"]





function fetchValidationConf
 return sValidationConf
end fetchValidationConf



--| END OF validation.lc
--| Location: ./system/application/config/validation.lc
----------------------------------------------------------------------
</code></pre>




<a href="#top">Top of Page</a>


<a name="theviewfiles"></a>

<h2>The View Files</h2>

<p>Create the following view files and save them to a folder called "auth" in your system/application/views/ folder.</p>

<p>We use multiple views nested in a parent view file. The header view, name it "headerView.lc", is included in all parent views (one fits all):</p>


<pre><code>&lt;!DOCTYPE html>
&lt;html>
&lt;head>
	&lt;meta charset="utf-8">

	&lt;title>[[gData["pageTitle"] ]]&lt;/title>
	&lt;? return rigCssAsset("authentication.css") ?>

&lt;/head>
</code></pre>



<p>The view which represents the admin page called "authView.lc":</p>


<pre><code>&lt;body>

	&lt;div id="container" class="adminPage">
		&lt;header>
			&lt;h1>Users&lt;/h1>
		&lt;/header>

		&lt;div id="main" role="main">


		&lt;div id="infoMessage">[[gData["message"] ]]&lt;/div>

			[[gData["usersTable"] ]]

			&lt;p>[[gData["newUserLink"] ]]&lt;/p>

			&lt;p>[[gData["logoutLink"] ]]&lt;/p>

		&lt;/div>
	&lt;/div> &lt;!--! end of #container -->

&lt;/body>
&lt;/html>	
</code></pre>


<p>The parent view of the admin page called "authMainView.lc":</p>


<pre><code>&lt;?
put rigLoadView("auth/headerView", TRUE) into tHeader
put rigLoadView("auth/authView", TRUE) into tContent
return tHeader && tContent
?>
</code></pre>



<p>The view used to change a password. It is called "changePasswordView.lc":</p>


<pre><code>&lt;body>

	&lt;div id="container" class="changePassword">
    &lt;header>
		&lt;h1>Change Password&lt;/h1>
    &lt;/header>

    &lt;div id="main" role="main">

		&lt;div id="infoMessage">[[gData["message"] ]]&lt;/div>


		[[gData["formOpen"] ]]
		[[gData["fsOpen"] ]]

		&lt;div>[[gData["oldPasswordLabel"] ]] [[gData["oldPassword"] ]]&lt;/div>

		&lt;div>[[gData["newPasswordLabel"] ]] [[gData["newPassword"] ]]&lt;/div>

		&lt;div>[[gData["newPasswordConfirmLabel"] ]] [[gData["newPasswordConfirm"] ]]&lt;/div>

		[[gData["hiddenUserID"] ]]


		&lt;div>[[gData["changeSubmit"] ]]&lt;/div>

		[[gData["fsClose"] ]]
		&lt;/form>

		&lt;/div>
	&lt;/div> &lt;!--! end of #container -->

&lt;/body>
&lt;/html>
</code></pre>


<p>"changePasswordMainView.lc", the parent view of the view above:</p>


<pre><code>&lt;?
put rigLoadView("auth/headerView", TRUE) into tHeader
put rigLoadView("auth/changePasswordView", TRUE) into tContent
return tHeader && tContent
?>
</code></pre>


<p>The "createUserView.lc" view:</p>


<pre><code>&lt;body>

	&lt;div id="container" class="createUser">
    &lt;header>
		&lt;h1>Create User&lt;/h1>
		&lt;p>Please enter the users information below.&lt;/p>
    &lt;/header>

    &lt;div id="main" role="main">

			&lt;div id="infoMessage">[[gData["message"] ]]&lt;/div>

			[[gData["formOpen"] ]]
			[[gData["fsOpen"] ]]


			&lt;div>[[gData["firstLabel"] ]] [[gData["first"] ]]&lt;/div>

			&lt;div>[[gData["lastLabel"] ]] [[gData["last"] ]]&lt;/div>

			&lt;div>[[gData["companyLabel"] ]] [[gData["company"] ]]&lt;/div>

			&lt;div>[[gData["emailLabel"] ]] [[gData["email"] ]]&lt;/div>

			&lt;div>[[gData["phoneLabel"] ]] [[gData["phone"] ]]&lt;/div>

			&lt;div>[[gData["passwordLabel"] ]] [[gData["password"] ]]&lt;/div>

			&lt;div>[[gData["passwordConfirmLabel"] ]] [[gData["passwordConfirm"] ]]&lt;/div>

			&lt;div>[[gData["createSubmit"] ]]&lt;/div>

		[[gData["fsClose"] ]]
			&lt;/form>

    	&lt;/div>
	&lt;/div> &lt;!--! end of #container -->

&lt;/body>
&lt;/html>
</code></pre>


<p>The parent view "createUserMainView.lc":</p>


<pre><code>&lt;?
put rigLoadView("auth/headerView", TRUE) into tHeader
put rigLoadView("auth/createUserView", TRUE) into tContent
return tHeader && tContent
?>
</code></pre>



<p>The "deactivateUserView.lc" view:</p>

<pre><code>&lt;body>

	&lt;div id="container" class="deactivate">
		&lt;header>
			&lt;h1>Deactivate User&lt;/h1>
			&lt;p>Are you sure you want to deactivate the user '[[gData["userName"] ]]'&lt;/p>
		&lt;/header>


		&lt;div id="main" role="main">

		[[gData["formOpen"] ]]
		[[gData["fsOpen"] ]]

			[[gData["hiddenUserID"] ]]
		&lt;div>[[gData["confirmRadioYesLabel"] ]] &lt;input type="radio" name="confirm" value="yes" [[gData["confirmYes"] ]] />&lt;/div>
		&lt;div>[[gData["confirmRadioNoLabel"] ]] &lt;input type="radio" name="confirm" value="no" [[gData["confirmNo"] ]] />&lt;/div>

 		&lt;div>[[gData["deactivateSubmit"] ]]&lt;/div>

		[[gData["fsClose"] ]]
		&lt;/form>
		&lt;/div>
	&lt;/div> &lt;!--! end of #container -->

&lt;/body>
&lt;/html>
</code></pre>



<p>The parent view "deactivateUserMainView.lc":</p>

<pre><code>&lt;?
put rigLoadView("auth/headerView", TRUE) into tHeader
put rigLoadView("auth/deactivateUserView", TRUE) into tContent
return tHeader && tContent
?>
</code></pre>




<p>The "deleteUserView.lc" view:</p>


<pre><code>&lt;body>

	&lt;div id="container" class="delete">
		&lt;header>
			&lt;h1>Delete User&lt;/h1>
			&lt;p>Are you sure you want to delete the user '[[gData["userName"] ]]'&lt;/p>
		&lt;/header>


	&lt;div id="main" role="main">

	[[gData["formOpen"] ]]
	[[gData["fsOpen"] ]]

	[[gData["hiddenUserID"] ]]
	&lt;div>[[gData["confirmRadioYesLabel"] ]] &lt;input type="radio" name="confirm" value="yes" [[gData["confirmYes"] ]] />&lt;/div>
	&lt;div>[[gData["confirmRadioNoLabel"] ]] &lt;input type="radio" name="confirm" value="no" [[gData["confirmNo"] ]] />&lt;/div>

	&lt;div>[[gData["deleteSubmit"] ]]&lt;/div>

	[[gData["fsClose"] ]]
	&lt;/form>
	&lt;/div>
	&lt;/div> &lt;!--! end of #container -->

&lt;/body>
&lt;/html>
</code></pre>



<p>The parent view "deleteUserMainView.lc":</p>

<pre><code>&lt;?
put rigLoadView("auth/headerView", TRUE) into tHeader
put rigLoadView("auth/deleteUserView", TRUE) into tContent
return tHeader && tContent
?>
</code></pre>



<p>The "editUserView.lc" view:</p>


<pre><code>&lt;body>

&lt;div id="container" class="editUser">
   &lt;header>
		&lt;h1>Edit User&lt;/h1>
		&lt;p>Please enter the users information below.&lt;/p>
   &lt;/header>

   &lt;div id="main" role="main">

		&lt;div id="infoMessage">[[gData["message"] ]]&lt;/div>

		[[gData["formOpen"] ]]
		[[gData["fsOpen"] ]]


		&lt;div>[[gData["firstLabel"] ]] [[gData["first"] ]]&lt;/div>

		&lt;div>[[gData["lastLabel"] ]] [[gData["last"] ]]&lt;/div>

		&lt;div>[[gData["companyLabel"] ]] [[gData["company"] ]]&lt;/div>

		&lt;div>[[gData["phoneLabel"] ]] [[gData["phone"] ]]&lt;/div>

		&lt;div>[[gData["passwordLabel"] ]] [[gData["password"] ]]&lt;/div>

		&lt;div>[[gData["passwordConfirmLabel"] ]] [[gData["passwordConfirm"] ]]&lt;/div>

		[[gData["hiddenUserID"] ]]

		&lt;div>[[gData["editSubmit"] ]]&lt;/div>

		[[gData["fsClose"] ]]
		&lt;/form>

    &lt;/div>
	&lt;/div> &lt;!--! end of #container -->

&lt;/body>
&lt;/html>
</code></pre>



<p>The parent view "editUserMainView.lc":</p>

<pre><code>&lt;?
put rigLoadView("auth/headerView", TRUE) into tHeader
put rigLoadView("auth/editUserView", TRUE) into tContent
return tHeader && tContent
?>
</code></pre>


<p>The "forgotPasswordView.lc" view:</p>


<pre><code>&lt;body>

	&lt;div id="container" class="forgotPassword">
    &lt;header>
		&lt;h1>Forgot Password&lt;/h1>
		&lt;p>Please enter your email address so we can send you an email to reset your password.&lt;/p>
    &lt;/header>

    &lt;div id="main" role="main">

		&lt;div id="infoMessage">[[gData["message"] ]]&lt;/div>


		[[gData["formOpen"] ]]
		[[gData["fsOpen"] ]]

		&lt;div>[[gData["emailLabel"] ]] [[gData["email"] ]]&lt;/div>

		&lt;div>[[gData["forgotSubmit"] ]]&lt;/div>

		[[gData["fsClose"] ]]
		&lt;/form>

	&lt;/div>
	&lt;/div> &lt;!--! end of #container -->

&lt;/body>
&lt;/html>
</code></pre>


<p>The parent view "forgotPasswordMainView.lc":</p>

<pre><code>&lt;?
put rigLoadView("auth/headerView", TRUE) into tHeader
put rigLoadView("auth/forgotPasswordView", TRUE) into tContent
return tHeader && tContent
?>
</code></pre>


<p>The "loginView.lc" view:</p>


<pre><code>&lt;body>

	&lt;div id="container" class="login">
		&lt;header>
			&lt;h1>Login&lt;/h1>
			&lt;p>Please login with your email/username and password below.&lt;/p>
		&lt;/header>

		&lt;div id="main" role="main">
			&lt;div id="infoMessage">[[gData["message"] ]]&lt;/div>

		[[gData["formOpen"] ]]
		[[gData["fsOpen"] ]]

		&lt;div>[[gData["identityLabel"] ]] [[gData["identity"] ]]&lt;/div>

		&lt;div>[[gData["passwordLabel"] ]] [[gData["loginPassword"] ]]&lt;/div>

		&lt;div>[[gData["rememberLabel"] ]] &lt;input type="checkbox" name="remember[]" value="1" [[gData["chbRemember"] ]] />&lt;/div>

		&lt;div>[[gData["loginSubmit"] ]]&lt;/div>


		[[gData["fsClose"] ]]
		&lt;/form>
	&lt;/div>
	&lt;/div> &lt;!--! end of #container -->

&lt;/body>
&lt;/html>
</code></pre>



<p>The parent view "loginMainView.lc":</p>

<pre><code>&lt;?
put rigLoadView("auth/headerView", TRUE) into tHeader
put rigLoadView("auth/loginView", TRUE) into tContent
return tHeader && tContent
?>
</code></pre>



<p>The "resetPasswordView.lc" view:</p>


<pre><code>&lt;body>

	&lt;div id="container" class="resetPassword">
    	&lt;header>
			&lt;h1>Change Password&lt;/h1>
    	&lt;/header>

    &lt;div id="main" role="main">

	&lt;div id="infoMessage">[[gData["message"] ]]&lt;/div>


	[[gData["formOpen"] ]]
	[[gData["fsOpen"] ]]


	&lt;div>[[gData["newPasswordLabel"] ]] [[gData["newPassword"] ]]&lt;/div>

	&lt;div>[[gData["newPasswordConfirmLabel"] ]] [[gData["newPasswordConfirm"] ]]&lt;/div>

	[[gData["hiddenUserID"] ]]


	&lt;div>[[gData["resetSubmit"] ]]&lt;/div>

	[[gData["fsClose"] ]]
	&lt;/form>

	&lt;/div>
	&lt;/div> &lt;!--! end of #container -->

&lt;/body>
&lt;/html>
</code></pre>



<p>The parent view "resetPasswordMainView.lc":</p>

<pre><code>&lt;?
put rigLoadView("auth/headerView", TRUE) into tHeader
put rigLoadView("auth/resetPasswordView", TRUE) into tContent
return tHeader && tContent
?>
</code></pre>




<a href="#top">Top of Page</a>

<a name="cssfile"></a>

<h2>The CSS File</h2>


<p>Create a CSS file called authentication.css, in it place the following code and save it to your assets/css/ folder:</p>


<pre><code>body {
	margin: 40px;
	padding: 0;
	font-family: "Lucida Grande", Lucida, Verdana, Geneva, Sans-serif;
	font-size: 12px;
	color: #888;
	background-color: #fff;
}
#container {
	margin-left: auto;
	margin-right: auto;
}
#container.login,
#container.editUser,
#container.createUser,
#container.changePassword,
#container.forgotPassword,
#container.resetPassword {
	width: 400px;
}
#container.adminPage {
	width: 610px;
}
#container.deactivate, #container.delete {
	width: 350px;
}
.formLabel {
	display: inline-block;
	width:	160px;
	margin-bottom: 10px;
}
table {
	width: 600px;
}
th {
	text-align: left;
}
button a {
	text-decoration: none;
}
</code></pre>

<h2>Try it!</h2>

<p>After having installed all those sample files</p>
<ul><li>go to your authentication page (yourDomain.com/auth/) and login as admin using "admin@admin.com" as user id and "revigniter" as password.</li>
	<li>Create a new user by clicking on "Create New User". Use your own email address so that you can verify that all works as expected.</li>
	<li>Log out.</li>
	<li>Check your email account and activate your test registration.</li>
	<li>Go to yourDomain.com/auth/logout/ to log out.</li>
	<li>Go to yourDomain.com/auth/forgotPassword, enter your email address and check your email account again to reset your password.</li>
	<li>Activate / deactivate users using the admin account.</li>
	<li>In short: Have fun.</li></ul>




</div>
<!-- END CONTENT -->

</div> <!-- /container -->


<div id="footer">
	<div class="container">
<p>
Previous Topic:&nbsp;&nbsp;<a href="../general/security.html"> Security</a>
<span class="separator"> :: </span>
<a href="#top">Top of Page</a><span class="separator"> :: </span>
<a href="../index.html">User Guide Home</a><span class="separator"> :: </span>
Next Topic:&nbsp;&nbsp;<a href="benchmark.html">Benchmarking Library</a>
</p>
<p><a href="http://revigniter.com/">revIgniter</a><span class="separator"> :: </span>Copyright &#169; 2009 - 2016<span class="separator"> :: </span><a href="http://revigniter.com/">dimensionB Bitter u. Bitter GmbH</a></p>
	</div>
</div>

<script src="../js/jquery-1.9.1.min.js"></script>
<script src="../js/bootstrap.min.js"></script>

<script src="../js/highlight.pack.js"></script>
 <script>
  	hljs.tabReplace = '    ';
  	hljs.initHighlightingOnLoad();
 </script>

<script src="../js/script-ck.js"></script>
<script>$(function() { Slidebox.init(); });</script>


</body>
</html>